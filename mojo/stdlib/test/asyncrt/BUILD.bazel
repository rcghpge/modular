load("//bazel:api.bzl", "mojo_test")

_TEST_DATA = [
    "asyncrt_test_utils.mojo",
]

_CPU_TESTS = [
    "smoke.mojo",
    "copies.mojo",
    "host_mapped.mojo",
    "timing.mojo",
    "memset.mojo",
]

_EXTRA_CONSTRAINTS = {
    "hip.mojo": ["//:amd_gpu"],
    "cuda.mojo": ["//:nvidia_gpu"],
    "capture.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2399
    "copies.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: GEX-2562
    "smoke.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: GEX-2554
    "elementwise.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "function.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2433
    "function_regress.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2433
}

[
    mojo_test(
        name = src + ".test",
        srcs = _TEST_DATA + [src],
        copts = [
            "-D",
            "MODULAR_ASYNCRT_DEVICE_CONTEXT_V2=gpu",
        ],
        exec_properties = {
            "test.resources:gpu-memory": "1.5",
        },
        main = src,
        tags = ["gpu"],
        target_compatible_with = ["//:has_gpu"] + _EXTRA_CONSTRAINTS.get(src, []),
        deps = [
            "@mojo//:layout",
            "@mojo//:stdlib",
            "@mojo//:tensor_internal",
        ],
    )
    for src in glob(
        ["**/*.mojo"],
        exclude = _TEST_DATA,
    )
]

[
    mojo_test(
        name = src + ".cpu.test",
        srcs = _TEST_DATA + [src],
        copts = [
            "-D",
            "MODULAR_ASYNCRT_DEVICE_CONTEXT_V2=cpu",
        ],
        main = src,
        deps = [
            "@mojo//:layout",
            "@mojo//:stdlib",
            "@mojo//:tensor_internal",
        ],
    )
    for src in _CPU_TESTS
]
