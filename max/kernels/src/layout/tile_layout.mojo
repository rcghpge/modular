# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #
"""Provides a mixed compile-time/runtime layout system for tensor memory mapping.

This module provides a layout system where some dimensions can be known at
compile time and others determined at runtime, enabling ergonomic layout
definitions while maintaining performance through compile-time specialization.

Key components:

- [`TensorLayout`](/mojo/kernels/layout/tile_layout/TensorLayout): Trait
  defining the interface for all mixed layouts.
- [`Layout`](/mojo/kernels/layout/tile_layout/Layout): Primary struct
  implementing a layout with mixed compile-time and runtime dimensions.
- [`row_major`](/mojo/kernels/layout/tile_layout/row_major): Create a
  row-major layout from a shape.
- [`col_major`](/mojo/kernels/layout/tile_layout/col_major): Create a
  column-major layout from a shape.
- [`blocked_product`](/mojo/kernels/layout/tile_layout/blocked_product):
  Create a hierarchical blocked layout from block and tiler layouts.
- [`zipped_divide`](/mojo/kernels/layout/tile_layout/zipped_divide): Divide
  a layout into inner and outer components by a tile shape.
- [`coalesce`](/mojo/kernels/layout/tile_layout/coalesce): Simplify a
  layout by merging dimensions with contiguous strides.

You can import these APIs from the `layout` package:

```mojo
from layout.tile_layout import Layout, TensorLayout, row_major, col_major
```
"""

from std.math.uutils import udivmod_unchecked

from std.builtin.variadics import (
    Variadic,
    _ReduceVariadicAndIdxToVariadic,
    _ReduceVariadicAndIdxToValue,
)

from .coord import (
    ComptimeInt,
    Idx,
    Coord,
    CoordLike,
    RuntimeInt,
    DynamicCoord,
    crd2idx,
    idx2crd,
    coord_to_int_tuple,
    _IntToComptimeInt,
    _CoordToDynamic,
    _Divide,
    _Multiply,
    _MultiplyByScalar,
    _Flattened,
)
from .int_tuple import IntTuple
from .layout import Layout as LegacyLayout


@always_inline("nodebug")
def _divide_by_stride[StrideType: CoordLike](idx: Int, stride_val: Int) -> Int:
    """Divide idx by stride, specializing for compile-time known values."""
    comptime if StrideType.is_static_value and StrideType.static_value == 1:
        return idx
    elif StrideType.is_static_value:
        var q, _ = udivmod_unchecked(idx, StrideType.static_value)
        return q
    else:
        var q, _ = udivmod_unchecked(idx, stride_val)
        return q


@always_inline("nodebug")
def _mod_by_shape[ShapeType: CoordLike](val: Int, shape_val: Int) -> Int:
    """Compute val % shape, specializing for compile-time known values.

    When the shape is compile-time known, uses the static value so LLVM can
    constant-fold. Special-cases shape==1 to return 0 directly.
    """
    comptime if ShapeType.is_static_value and ShapeType.static_value == 1:
        return 0
    elif ShapeType.is_static_value:
        _, var r = udivmod_unchecked(val, ShapeType.static_value)
        return r
    else:
        _, var r = udivmod_unchecked(val, shape_val)
        return r


trait TensorLayout(TrivialRegisterPassable):
    """Trait defining the interface for mixed compile-time/runtime layouts.

    Implementors map logical multi-dimensional coordinates to linear memory
    indices, with support for dimensions that are known at compile time or
    determined at runtime.
    """

    comptime rank: Int
    """The number of dimensions in the layout."""
    comptime flat_rank: Int
    """The number of dimensions after flattening nested coordinates."""
    comptime shape_known: Bool
    """Whether all shape dimensions are known at compile time."""
    comptime stride_known: Bool
    """Whether all stride dimensions are known at compile time."""
    comptime all_dims_known: Bool = Self.shape_known and Self.stride_known
    """Whether all shape and stride dimensions are known at compile time."""
    comptime static_shape[i: Int]: Int
    """Returns the compile-time value of the i-th shape dimension.

    Parameters:
        i: The dimension index.
    """
    comptime static_stride[i: Int]: Int
    """Returns the compile-time value of the i-th stride dimension.

    Parameters:
        i: The dimension index.
    """

    comptime static_product: Int
    """The compile-time product of all shape dimensions."""

    comptime static_cosize: Int
    """The compile-time size of the memory region spanned by the layout."""

    comptime _shape_types: Variadic.TypesOfTrait[CoordLike]
    comptime _stride_types: Variadic.TypesOfTrait[CoordLike]

    def shape[i: Int](self) -> Self._shape_types[i]:
        """Returns the i-th shape dimension.

        Parameters:
            i: The dimension index.

        Returns:
            The shape value for dimension `i`.
        """
        ...

    def stride[i: Int](self) -> Self._stride_types[i]:
        """Returns the i-th stride dimension.

        Parameters:
            i: The dimension index.

        Returns:
            The stride value for dimension `i`.
        """
        ...

    def product(self) -> Int:
        """Returns the total number of elements in the layout's domain.

        Returns:
            The product of all shape dimensions.
        """
        ...

    def size(self) -> Int:
        """Returns the total number of elements. Alias for `product()`.

        Returns:
            The product of all shape dimensions.
        """
        ...

    def __call__[
        index_type: CoordLike,
        *,
        linear_idx_type: DType = DType.int64,
    ](self, index: index_type) -> Scalar[linear_idx_type]:
        """Maps a logical coordinate to a linear memory index.

        Parameters:
            index_type: The coordinate type.
            linear_idx_type: The data type for the returned linear index.

        Args:
            index: The logical coordinates to map.

        Returns:
            The linear memory index corresponding to the given coordinates.
        """
        ...

    def idx2crd[
        *,
        out_dtype: DType = DType.int64,
    ](self, idx: Int) -> DynamicCoord[out_dtype, Self.rank]:
        """Maps a linear memory index back to logical coordinates.

        This is the inverse of `__call__` (crd2idx). Given a linear index,
        it computes the corresponding multi-dimensional coordinates.

        Parameters:
            out_dtype: The data type for the output coordinate values.

        Args:
            idx: The linear memory index to convert to coordinates.

        Returns:
            A Coord containing the logical coordinates corresponding to
            the linear index.

        Examples:
            For a layout with shape (3, 4) and row-major strides:
            - layout.idx2crd(0) returns (0, 0).
            - layout.idx2crd(5) returns (1, 1).
            - layout.idx2crd(11) returns (2, 3).
        """
        ...

    def shape_coord(self) -> Coord[*Self._shape_types]:
        """Returns the full shape as a `Coord`.

        Returns:
            A Coord containing all shape dimensions.
        """
        ...

    def stride_coord(self) -> Coord[*Self._stride_types]:
        """Returns the full stride as a `Coord`.

        Returns:
            A Coord containing all stride dimensions.
        """
        ...

    def transpose(
        self,
    ) -> Layout[
        Variadic.reverse[*Self._shape_types],
        Variadic.reverse[*Self._stride_types],
    ]:
        """Transposes the layout by reversing the order of dimensions.

        For an n-dimensional layout, this reverses the order of both shapes
        and strides. For 2D layouts, this swaps rows and columns.

        Returns:
            A new Layout with transposed dimensions.
        """
        ...

    def make_dynamic[
        dtype: DType
    ](self) -> Layout[
        _CoordToDynamic[dtype, *Self._shape_types],
        _CoordToDynamic[dtype, *Self._stride_types],
    ]:
        """Converts all dimensions to runtime values of the given dtype.

        Parameters:
            dtype: The data type for the resulting `RuntimeInt` values.

        Returns:
            A new Layout with all dimensions as `RuntimeInt[dtype]`.
        """
        ...


comptime RowMajorLayout[*shape_types: CoordLike] = Layout[
    shape_types, _RowMajor[*shape_types]
]
"""A `Layout` with row-major (C-order) strides computed from the shape.

Parameters:
    shape_types: The types for the shape dimensions.
"""


struct Layout[
    shape_types: Variadic.TypesOfTrait[CoordLike],
    stride_types: Variadic.TypesOfTrait[CoordLike],
](ImplicitlyCopyable, TensorLayout, TrivialRegisterPassable, Writable):
    """A layout that supports mixed compile-time and runtime dimensions.

    This layout provides a unified interface for layouts where some dimensions
    are known at compile time and others are determined at runtime. It enables
    more ergonomic layout definitions while maintaining performance.

    A Layout's shape and strides must be non-negative.

    Parameters:
        shape_types: The types for the shape dimensions.
        stride_types: The types for the stride dimensions.
    """

    var _shape: Coord[*Self.shape_types]
    """The shape of the layout as a Coord."""

    var _stride: Coord[*Self.stride_types]
    """The stride of the layout as a Coord."""

    comptime rank = Variadic.size(Self.shape_types)
    """The number of dimensions in the layout."""
    comptime flat_rank = Variadic.size(_Flattened[*Self.shape_types])
    """The number of dimensions after flattening nested coordinates."""
    comptime shape_known = Coord[*Self.shape_types].all_dims_known
    """Whether all shape dimensions are known at compile time."""
    comptime stride_known = Coord[*Self.stride_types].all_dims_known
    """Whether all stride dimensions are known at compile time."""
    comptime _flat_shape_types = _Flattened[*Self.shape_types]
    comptime _flat_stride_types = _Flattened[*Self.stride_types]
    comptime static_shape[i: Int]: Int = Self._flat_shape_types[i].static_value
    """Returns the compile-time value of the i-th flattened shape dimension.

    Parameters:
        i: The dimension index.
    """
    comptime static_stride[i: Int]: Int = Self._flat_stride_types[
        i
    ].static_value
    """Returns the compile-time value of the i-th flattened stride dimension.

    Parameters:
        i: The dimension index.
    """
    comptime _shape_types: Variadic.TypesOfTrait[CoordLike] = Self.shape_types
    comptime _stride_types: Variadic.TypesOfTrait[CoordLike] = Self.stride_types

    comptime static_product = Coord[*Self._flat_shape_types].static_product
    """The compile-time product of all shape dimensions (handles nested Coords)."""

    comptime static_cosize = _StaticCosize[
        Self._flat_shape_types, Self._flat_stride_types
    ]
    """The compile-time size of the memory region spanned by the layout."""

    @always_inline("nodebug")
    def __init__(out self):
        """Default-initialize a layout from its compile-time type parameters.

        Each dimension is initialized to its default value: compile-time
        dimensions (`ComptimeInt`) get their static value, runtime dimensions
        (`RuntimeInt`) get 0. This is useful for constructing a fully-static
        layout purely from its type, e.g. ``UpcastLayout[MyLayout, 2]()``.
        """
        self._shape = Coord[*Self.shape_types]()
        self._stride = Coord[*Self.stride_types]()

    @always_inline("nodebug")
    def __init__(
        out self,
        shape: Coord[*Self.shape_types],
        stride: Coord[*Self.stride_types],
    ):
        """Initialize a layout with shape and stride.

        Args:
            shape: The shape as a Coord.
            stride: The stride as a Coord.
        """
        comptime assert (
            type_of(shape).__len__() == type_of(stride).__len__()
        ), String(
            (
                "Shape and stride must have the same length, but got shape"
                " length: "
            ),
            type_of(shape).__len__(),
            " stride length: ",
            type_of(stride).__len__(),
        )
        self._shape = shape
        self._stride = stride

    def __call__[
        index_type: CoordLike,
        *,
        linear_idx_type: DType = DType.int64,
    ](self, index: index_type) -> Scalar[linear_idx_type]:
        """Maps a logical coordinate to a linear memory index.

        Supports hierarchical indexing where the coordinate structure can
        differ from the shape structure. For a layout with shape (4, (3, 2)):

        - (1, (1, 1)): exact structure match, each element maps directly.
        - (1, 1): rank-matching, the scalar 1 is decomposed within the
          nested (3, 2) sub-dimension.
        - (1): scalar index decomposed across all dimensions.

        Parameters:
            index_type: The coordinate type.
            linear_idx_type: The data type for the returned linear index.

        Args:
            index: The logical coordinates to map.

        Returns:
            The linear memory index corresponding to the given coordinates.
        """
        comptime index_len = index_type.__len__()

        comptime if index_len == Self.rank:
            # Hierarchical: each coord element maps to one shape dimension.
            # If a shape dimension is nested (e.g., (3, 2)) and the
            # corresponding coord element is a scalar, crd2idx decomposes
            # the scalar within that sub-dimension.
            return crd2idx[out_type=linear_idx_type](
                index, self._shape, self._stride
            )
        elif index_type.is_tuple and index_len > Self.rank:
            # More coord elements than shape dimensions: flatten the coord
            # and strides, then compute a direct element-wise dot product.
            var flat_idx = index.tuple().flatten()
            var flat_stride = self._stride.flatten()
            var result: Scalar[linear_idx_type] = 0

            comptime flat_len = type_of(flat_idx).__len__()
            comptime for i in range(flat_len):
                result += Scalar[linear_idx_type](
                    flat_idx[i].value() * flat_stride[i].value()
                )

            return result
        else:
            # Scalar or single-element coord: decompose against full shape.
            return crd2idx[out_type=linear_idx_type](
                index, self._shape, self._stride
            )

    @always_inline("nodebug")
    def idx2crd[
        *,
        out_dtype: DType = DType.int64,
    ](self, idx: Int) -> DynamicCoord[out_dtype, Self.rank]:
        """Maps a linear memory index back to logical coordinates.

        This is the inverse of `__call__` (crd2idx). Given a linear index,
        it computes the corresponding multi-dimensional coordinates using
        the per-element formula: ``coord[i] = (idx // stride[i]) % shape[i]``.

        Parameters:
            out_dtype: The data type for the output coordinate values.

        Args:
            idx: The linear memory index to convert to coordinates.

        Returns:
            A Coord containing the logical coordinates corresponding to the linear index.

        Examples:
            For a layout with shape (3, 4) and row-major strides:
            - layout.idx2crd(0) returns (0, 0).
            - layout.idx2crd(5) returns (1, 1).
            - layout.idx2crd(11) returns (2, 3).
        """
        comptime ResultType = DynamicCoord[out_dtype, Self.rank]
        var result = ResultType()
        var shape_t = self._shape.tuple()
        var stride_t = self._stride.tuple()

        comptime for i in range(Self.rank):
            var divided = _divide_by_stride[Self.stride_types[i]](
                idx, stride_t[i].value()
            )
            var coord_val = _mod_by_shape[Self.shape_types[i]](
                divided, shape_t[i].value()
            )
            UnsafePointer(to=result[i]).init_pointee_copy(
                rebind[ResultType.element_types[i]](
                    RuntimeInt[out_dtype](Scalar[out_dtype](coord_val))
                )
            )
        return result

    @always_inline("nodebug")
    def product(self) -> Int:
        """Returns the total number of elements in the layout's domain.

        For a layout with shape (m, n), this returns m * n, representing
        the total number of valid coordinates in the layout.

        Returns:
            The total number of elements in the layout.
        """
        return self._shape.product()

    @always_inline("nodebug")
    def size(self) -> Int:
        """Returns the total number of elements in the layout's domain.

        Alias for `product()`. Compatible with the legacy Layout API.

        Returns:
            The total number of elements in the layout.
        """
        return self.product()

    @always_inline("nodebug")
    def cosize[
        linear_idx_type: DType = DType.int64
    ](self) -> Scalar[linear_idx_type]:
        """Returns the size of the memory region spanned by the layout.

        For a layout with shape `(m, n)` and stride `(r, s)`, this returns
        `(m-1)*r + (n-1)*s + 1`, representing the memory footprint.

        Parameters:
            linear_idx_type: The data type for the returned size value.

        Returns:
            The size of the memory region required by the layout.
        """
        return (
            self[linear_idx_type=linear_idx_type](Idx(self.product() - 1)) + 1
        )

    @always_inline("nodebug")
    def to_layout(self) -> LegacyLayout:
        """Converts this mixed layout to a legacy `Layout` using `IntTuple`.

        Returns:
            A legacy `Layout` with the same shape and stride.
        """
        return LegacyLayout(
            coord_to_int_tuple(self._shape),
            coord_to_int_tuple(self._stride),
        )

    @staticmethod
    def to_legacy_layout() -> LegacyLayout:
        """Converts this layout type to a legacy `Layout` via type-level extraction.

        All dimensions must be known at compile time. Uses direct `IntTuple`
        construction (no `append`) for fast compile times.

        Returns:
            A legacy `Layout` with the same shape and stride.
        """
        comptime assert (
            Self.all_dims_known
        ), "to_legacy_layout requires all dimensions to be compile-time known"
        return LegacyLayout(
            _types_to_int_tuple[Self._shape_types](),
            _types_to_int_tuple[Self._stride_types](),
        )

    @always_inline("nodebug")
    def reverse(
        self,
    ) -> Layout[
        Variadic.reverse[*Self.shape_types],
        Variadic.reverse[*Self.stride_types],
    ]:
        """Reverse the order of dimensions in the layout.

        Turns row-major into column-major ordering where the stride-1
        dimension comes first, enabling coalesced scalar iteration.

        Returns:
            A new Layout with shape and stride Coords reversed.
        """
        return Layout(self._shape.reverse(), self._stride.reverse())

    @always_inline("nodebug")
    def transpose(
        self,
    ) -> Layout[
        Variadic.reverse[*Self.shape_types],
        Variadic.reverse[*Self.stride_types],
    ]:
        """Transposes the layout by reversing the order of dimensions.

        For an n-dimensional layout, this reverses the order of both shapes
        and strides. For 2D layouts, this swaps rows and columns, converting
        row-major to column-major and vice versa.

        Returns:
            A new Layout with transposed dimensions.

        Example:

        ```mojo
        from layout.tile_layout import row_major

        var layout = row_major[3, 4]()  # shape (3,4), stride (4,1)
        var transposed = layout.transpose()  # shape (4,3), stride (1,4)
        ```
        """
        return self.reverse()

    @always_inline("nodebug")
    def make_dynamic[
        dtype: DType
    ](self) -> Layout[
        _CoordToDynamic[dtype, *Self.shape_types],
        _CoordToDynamic[dtype, *Self.stride_types],
    ]:
        """Convert all elements in shape and stride to RuntimeInt[dtype].

        Parameters:
            dtype: The data type for the resulting RuntimeInt values.

        Returns:
            A new Layout where all elements in shape and stride are
            converted to RuntimeInt[dtype].

        Examples:
            ```mojo
            from layout.tile_layout import row_major
            var layout = row_major[3, 4]()  # All compile-time
            var dynamic = layout.make_dynamic[DType.int64]()
            # dynamic has RuntimeInt[DType.int64] for all dimensions
            ```
        """
        return Layout(
            self._shape.make_dynamic[dtype](),
            self._stride.make_dynamic[dtype](),
        )

    @always_inline("nodebug")
    def shape[i: Int](self) -> Self._shape_types[i]:
        """Returns the i-th shape dimension.

        Parameters:
            i: The dimension index.

        Returns:
            The shape value for dimension `i`.
        """
        return self._shape[i]

    @always_inline("nodebug")
    def stride[i: Int](self) -> Self._stride_types[i]:
        """Returns the i-th stride dimension.

        Parameters:
            i: The dimension index.

        Returns:
            The stride value for dimension `i`.
        """
        return self._stride[i]

    @always_inline("nodebug")
    def shape_coord(self) -> Coord[*Self._shape_types]:
        """Returns the full shape as a `Coord`.

        Returns:
            A Coord containing all shape dimensions.
        """
        return self._shape

    @always_inline("nodebug")
    def stride_coord(self) -> Coord[*Self._stride_types]:
        """Returns the full stride as a `Coord`.

        Returns:
            A Coord containing all stride dimensions.
        """
        return self._stride

    @always_inline("nodebug")
    def write_to(self, mut writer: Some[Writer]):
        """Writes the Layout representation to a Writer.

        Args:
            writer: The object to write to.
        """
        writer.write(t"({self.shape_coord()}:{self.stride_coord()})")


# ===----------------------------------------------------------------------=== #
# Type-level Layout Conversion
# ===----------------------------------------------------------------------=== #


def _type_to_int_tuple[T: CoordLike]() -> IntTuple:
    """Convert a CoordLike type to an IntTuple via direct construction.

    For scalar types, returns IntTuple(static_value).
    For tuple types, recursively converts children using direct IntTuple
    construction (no append) for rank <= 2.
    """
    comptime if not T.is_tuple:
        return IntTuple(T.static_value)
    else:
        return _types_to_int_tuple[T.VariadicType]()


def _types_to_int_tuple[Types: Variadic.TypesOfTrait[CoordLike]]() -> IntTuple:
    """Convert variadic CoordLike types to an IntTuple.

    Uses direct IntTuple construction (no append) for rank 1-2.
    Falls back to append for rank > 2.
    """
    comptime N = Variadic.size(Types)
    comptime if N == 1:
        return _type_to_int_tuple[Types[0]]()
    elif N == 2:
        return IntTuple(
            _type_to_int_tuple[Types[0]](),
            _type_to_int_tuple[Types[1]](),
        )
    else:
        var result = IntTuple()
        comptime for i in range(N):
            result.append(_type_to_int_tuple[Types[i]]())
        return result


comptime _StaticCosizeReducer[
    Strides: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.ValuesOfType[Int],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.values[
    (From[idx].static_value - 1) * Strides[idx].static_value + Prev[0]
]


comptime _StaticCosize[
    Shapes: Variadic.TypesOfTrait[CoordLike],
    Strides: Variadic.TypesOfTrait[CoordLike],
] = _ReduceVariadicAndIdxToValue[
    BaseVal=Variadic.values[1],
    VariadicType=Shapes,
    Reducer=_StaticCosizeReducer[Strides=Strides, ...],
][
    0
]


comptime _RowMajor[*element_types: CoordLike] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=Variadic.reverse[*_UnwrapSingleTuple[*element_types]],
    Reducer=_RowMajorMapper,
]


comptime _UnwrapSingleTuple[*element_types: CoordLike] = element_types[
    0
].VariadicType if Variadic.size(element_types) == 1 and element_types[
    0
].is_tuple else element_types


comptime _RowMajorMapper[
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Variadic.types[T=CoordLike, ComptimeInt[1]] if idx
    == 0 else (
        Variadic.types[
            T=CoordLike,
            RuntimeInt[
                From[idx - 1]
                .DTYPE if not From[idx - 1]
                .is_static_value else Prev[0]
                .DTYPE
            ],
        ] if not From[idx - 1].is_static_value
        or not Prev[0].is_static_value else Variadic.types[
            T=CoordLike,
            ComptimeInt[From[idx - 1].static_value * Prev[0].static_value],
        ]
    ),
    Prev,
]


@always_inline
def row_major(var shape: Coord) -> RowMajorLayout[*shape.element_types]:
    """Creates a row-major layout from a shape `Coord`.

    Row-major means the rightmost dimension has stride 1, and each preceding
    dimension has stride equal to the product of all following dimensions.

    For shape (M, N, K):
    - row_major strides: (N*K, K, 1)
    - col_major strides: (1, M, M*N)

    Args:
        shape: The shape as a Coord.

    Returns:
        A Layout with row-major strides.
    """
    comptime RowMajorTypes = _RowMajor[*shape.element_types]
    comptime rank = Variadic.size(shape.element_types)

    var strides = Tuple[*RowMajorTypes]()

    comptime for i in range(rank):
        comptime idx = rank - 1 - i  # Process in reverse order
        var stride_ptr = UnsafePointer(to=strides[idx])

        comptime if i == 0:
            # Rightmost dimension always has stride 1
            comptime StrideType = RowMajorTypes[idx]
            stride_ptr.init_pointee_copy(rebind[StrideType](Idx[1]()))
        else:
            # Calculate stride as product of shape[idx+1] * stride[idx+1]
            comptime StrideType = RowMajorTypes[idx]

            comptime if StrideType.is_static_value:
                comptime stride_val = StrideType.static_value
                stride_ptr.init_pointee_copy(
                    rebind[StrideType](Idx[stride_val]())
                )
            else:
                var stride_val = (
                    shape[idx + 1].value() * strides[idx + 1].value()
                )
                stride_ptr.init_pointee_copy(
                    rebind[StrideType](
                        RuntimeInt[StrideType.DTYPE](
                            Scalar[StrideType.DTYPE](stride_val)
                        )
                    )
                )

    return Layout(shape, Coord(strides^))


@always_inline
def row_major[
    *element_types: CoordLike
](var *elements: *element_types) -> RowMajorLayout[*element_types]:
    """Creates a row-major layout from a shape `Coord`.

    Row-major means the rightmost dimension has stride 1, and each preceding
    dimension has stride equal to the product of all following dimensions.

    Parameters:
        element_types: The variadic pack of element types that implement `CoordLike`.

    Args:
        elements: The shape as a Coord.

    Returns:
        A Layout with row-major strides.
    """

    comptime RowMajorTypes = _RowMajor[*element_types]
    comptime rank = Variadic.size(element_types)

    var strides = Tuple[*RowMajorTypes]()

    # Compute row-major strides on the flattened shape
    # Row-major means rightmost dimension has stride 1,
    # and each preceding dimension has stride equal to the product of all following dimensions
    comptime for i in range(rank):
        comptime idx = rank - 1 - i  # Process in reverse order
        var stride_ptr = UnsafePointer(to=strides[idx])

        comptime if i == 0:
            # Rightmost dimension always has stride 1
            comptime StrideType = RowMajorTypes[idx]
            stride_ptr.init_pointee_copy(rebind[StrideType](Idx[1]()))
        else:
            # Calculate stride as product of shape[idx+1] * stride[idx+1]
            comptime StrideType = RowMajorTypes[idx]

            comptime if StrideType.is_static_value:
                # Stride is compile-time known (both shape and prev stride are compile-time)
                comptime stride_val = StrideType.static_value
                stride_ptr.init_pointee_copy(
                    rebind[StrideType](Idx[stride_val]())
                )
            else:
                # At least one is runtime, compute at runtime
                var stride_val = (
                    elements[idx + 1].value() * strides[idx + 1].value()
                )
                stride_ptr.init_pointee_copy(
                    rebind[StrideType](
                        RuntimeInt[StrideType.DTYPE](
                            Scalar[StrideType.DTYPE](stride_val)
                        )
                    )
                )

    return Layout(Coord(storage=elements^), Coord(strides^))


@always_inline("nodebug")
def row_major[*idxs: Int]() -> RowMajorLayout[*_IntToComptimeInt[*idxs]]:
    """Creates a row-major layout from compile-time shape dimensions.

    Parameters:
        idxs: The shape dimensions as compile-time integers.

    Returns:
        A Layout with row-major strides.
    """
    var shape = Coord[*_IntToComptimeInt[*idxs]]()
    return row_major(shape)


# ===----------------------------------------------------------------------=== #
# Column Major Layout
# ===----------------------------------------------------------------------=== #


comptime ColMajorLayout[*shape_types: CoordLike] = Layout[
    shape_types, _ColMajor[*shape_types]
]
"""A `Layout` with column-major (Fortran-order) strides computed from the shape.

Parameters:
    shape_types: The types for the shape dimensions.
"""


comptime _ColMajor[*element_types: CoordLike] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=Variadic.types[
        *_UnwrapSingleTuple[*element_types]
    ],  # Process in forward order
    Reducer=_ColMajorMapper,
]


comptime _ColMajorMapper[
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    Variadic.types[T=CoordLike, ComptimeInt[1]] if idx
    == 0 else (
        Variadic.types[
            T=CoordLike,
            RuntimeInt[
                From[idx - 1]
                .DTYPE if not From[idx - 1]
                .is_static_value else Prev[idx - 1]
                .DTYPE
            ],
        ] if not From[idx - 1].is_static_value
        or not Prev[idx - 1].is_static_value else Variadic.types[
            T=CoordLike,
            ComptimeInt[
                From[idx - 1].static_value * Prev[idx - 1].static_value
            ],
        ]
    ),
]


@always_inline
def col_major[
    *element_types: CoordLike
](var *elements: *element_types) -> ColMajorLayout[*element_types]:
    """Create a column-major layout from variadic arguments.

    Column-major means the first dimension has stride 1, and each subsequent
    dimension has stride equal to the product of all previous dimensions.

    Parameters:
        element_types: The variadic pack of element types that implement `CoordLike`.

    Args:
        elements: The shape dimensions.

    Returns:
        A Layout with column-major strides.
    """
    return col_major(Coord(storage=elements^))


@always_inline
def col_major(var shape: Coord) -> ColMajorLayout[*shape.element_types]:
    """Create a column-major layout from a shape.

    Column-major means the first dimension has stride 1, and each subsequent
    dimension has stride equal to the product of all previous dimensions.

    For shape (M, N, K):
    - row_major strides: (N*K, K, 1)
    - col_major strides: (1, M, M*N)

    Args:
        shape: The shape as a Coord.

    Returns:
        A Layout with column-major strides.
    """
    comptime ColMajorTypes = _ColMajor[*shape.element_types]
    comptime rank = Variadic.size(shape.element_types)

    var strides = Tuple[*ColMajorTypes]()

    # Compute column-major strides on the shape
    # Column-major means leftmost dimension has stride 1,
    # and each subsequent dimension has stride = product of all previous dimensions
    comptime for i in range(rank):
        var stride_ptr = UnsafePointer(to=strides[i])

        comptime if i == 0:
            # Leftmost dimension always has stride 1
            comptime StrideType = ColMajorTypes[i]
            stride_ptr.init_pointee_copy(rebind[StrideType](Idx[1]()))
        else:
            # Calculate stride as product of shape[i-1] * stride[i-1]
            comptime StrideType = ColMajorTypes[i]

            comptime if StrideType.is_static_value:
                # Stride is compile-time known
                comptime stride_val = StrideType.static_value
                stride_ptr.init_pointee_copy(
                    rebind[StrideType](Idx[stride_val]())
                )
            else:
                # At least one is runtime, compute at runtime
                var stride_val = shape[i - 1].value() * strides[i - 1].value()
                stride_ptr.init_pointee_copy(
                    rebind[StrideType](
                        RuntimeInt[StrideType.DTYPE](
                            Scalar[StrideType.DTYPE](stride_val)
                        )
                    )
                )

    return Layout(shape, Coord(strides^))


@always_inline("nodebug")
def col_major[*idxs: Int]() -> ColMajorLayout[*_IntToComptimeInt[*idxs]]:
    """Create a column-major layout from compile-time shape dimensions.

    Parameters:
        idxs: The shape dimensions as compile-time integers.

    Returns:
        A Layout with column-major strides.

    Example:

    ```mojo
    from layout.tile_layout import col_major

    var layout = col_major[3, 4]()
    # shape: (3, 4), stride: (1, 3)
    ```
    """
    var shape = Coord[*_IntToComptimeInt[*idxs]]()
    return col_major(shape)


@always_inline("nodebug")
def col_major(
    idx: ComptimeInt[...],
) -> Layout[
    shape_types=Variadic.types[type_of(idx)],
    stride_types=Variadic.types[ComptimeInt[1]],
]:
    """Creates a 1D column-major layout from a compile-time dimension.

    Args:
        idx: The shape dimension as a `ComptimeInt`.

    Returns:
        A 1D Layout with stride 1.
    """
    return Layout(Coord(idx), Coord(Idx[1]()))


@always_inline("nodebug")
def col_major(
    idx: RuntimeInt[...],
) -> Layout[
    shape_types=Variadic.types[type_of(idx)],
    stride_types=Variadic.types[ComptimeInt[1]],
]:
    """Creates a 1D column-major layout from a runtime dimension.

    Args:
        idx: The shape dimension as a `RuntimeInt`.

    Returns:
        A 1D Layout with stride 1.
    """
    return Layout(Coord(idx), Coord(Idx[1]()))


def zipped_divide[
    LayoutType: TensorLayout, //, tile: Coord
](layout: LayoutType) -> ZippedDivideLayout[LayoutType, tile.element_types]:
    """Divides a layout into inner (tile) and outer (number-of-tiles) parts.

    Given a layout and a tile shape, produces a hierarchical layout where the
    inner component has the tile shape with the original strides, and the outer
    component has shape = original_shape / tile with scaled strides.

    Parameters:
        LayoutType: The type of the input layout.
        tile: The tile shape to divide by.

    Args:
        layout: The layout to divide.

    Returns:
        A `ZippedDivideLayout` with inner and outer components.
    """
    var shape = layout.shape_coord()
    var outer_shape = Coord[
        *_Divide[LayoutType._shape_types, tile.element_types]
    ]()
    var outer_stride = Coord[
        *_Multiply[LayoutType._stride_types, tile.element_types]
    ]()
    var inner_shape = tile
    var inner_stride = layout.stride_coord()

    comptime for i in range(outer_shape.rank):
        comptime if (
            outer_shape.element_types[i].is_value
            and not outer_shape.element_types[i].is_static_value
        ):
            outer_shape[i] = rebind[outer_shape.element_types[i]](
                Scalar[outer_shape.element_types[i].DTYPE](
                    shape[i].value() // tile[i].value()
                )
            )

        comptime if (
            outer_stride.element_types[i].is_value
            and not outer_stride.element_types[i].is_static_value
        ):
            outer_stride[i] = rebind[outer_stride.element_types[i]](
                Scalar[outer_stride.element_types[i].DTYPE](
                    inner_stride[i].value() * tile[i].value()
                )
            )
    var out_layout = Layout(
        Coord(inner_shape, outer_shape), Coord(inner_stride, outer_stride)
    )
    return out_layout


comptime ZippedDivideLayout[
    LayoutType: TensorLayout,
    tile: Variadic.TypesOfTrait[CoordLike],
] = Layout[
    Variadic.types[
        T=CoordLike,
        Coord[*tile],  # inner_shape = tile
        Coord[
            *_Divide[LayoutType._shape_types, tile]
        ],  # outer_shape = shape / tile
    ],
    Variadic.types[
        T=CoordLike,
        Coord[*LayoutType._stride_types],  # inner_stride = original stride
        Coord[
            *_Multiply[LayoutType._stride_types, tile]
        ],  # outer_stride = stride * tile
    ],
]
"""Type alias for the result of `zipped_divide`.

Splits a layout into inner (tile-sized) and outer (number-of-tiles)
components. The result is a 2-level hierarchical layout where:

- ``inner_shape  = tile``
- ``outer_shape  = shape / tile``
- ``inner_stride = original stride``
- ``outer_stride = stride * tile``

For fully-static layouts, this can be used directly at the type level:

```mojo
comptime result = ZippedDivideLayout[type_of(my_layout), tile.element_types]()
```

Parameters:
    LayoutType: The input layout type.
    tile: Shape types of the tile used to divide the layout.
"""


# ===----------------------------------------------------------------------=== #
# Blocked Product
# ===----------------------------------------------------------------------=== #


comptime _BlockedProductShapeReducer[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    Variadic.types[
        T=CoordLike,
        Coord[
            BlockLayoutType._shape_types[idx],
            TilerLayoutType._shape_types[idx],
        ],
    ],
]

comptime _BlockedProductShapeTypes[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=BlockLayoutType._shape_types,
    Reducer=_BlockedProductShapeReducer[
        BlockLayoutType,
        TilerLayoutType,
        ...,
    ],
]

comptime _BlockedProductStrideReducer[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
    block_cosize: Int,
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    Variadic.types[
        T=CoordLike,
        Coord[
            BlockLayoutType._stride_types[idx],
            ComptimeInt[
                block_cosize * TilerLayoutType._stride_types[idx].static_value
            ],
        ],
    ],
]

comptime _BlockedProductStrideTypes[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=BlockLayoutType._stride_types,
    Reducer=_BlockedProductStrideReducer[
        BlockLayoutType,
        TilerLayoutType,
        Coord[*BlockLayoutType._shape_types].static_product,
        ...,
    ],
]

comptime _CoalescedBlockedProductShapeTypes[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=BlockLayoutType._shape_types,
    Reducer=_CoalescedBlockedShapeReducer[
        BlockLayoutType,
        TilerLayoutType,
        Coord[*BlockLayoutType._shape_types].static_product,
        ...,
    ],
]

comptime _CoalescedBlockedProductStrideTypes[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=BlockLayoutType._stride_types,
    Reducer=_CoalescedBlockedStrideReducer[
        BlockLayoutType,
        TilerLayoutType,
        Coord[*BlockLayoutType._shape_types].static_product,
        ...,
    ],
]

comptime BlockedProductLayout[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
    coalesce_output: Bool = False,
] = Layout[
    _CoalescedBlockedProductShapeTypes[
        BlockLayoutType, TilerLayoutType
    ] if coalesce_output else _BlockedProductShapeTypes[
        BlockLayoutType, TilerLayoutType
    ],
    _CoalescedBlockedProductStrideTypes[
        BlockLayoutType, TilerLayoutType
    ] if coalesce_output else _BlockedProductStrideTypes[
        BlockLayoutType, TilerLayoutType
    ],
]
"""Type alias for blocked product layout.

Creates a hierarchical layout by combining a block (inner) layout with a
tiler (outer) layout. The result zips corresponding dimensions so that
each mode ``i`` pairs ``block[i]`` with ``tiler[i]``:

- ``shape[i]  = (block.shape[i],  tiler.shape[i])``
- ``stride[i] = (block.stride[i], block.cosize * tiler.stride[i])``

When ``coalesce_output`` is True, contiguous inner/outer pairs per mode
are merged into flat dimensions (``block_shape[i] * block_stride[i] ==
outer_stride[i]``). This corresponds to the old
``blocked_product(..., coalesce_output=True)`` with ``keep_rank=True``.

For fully-static layouts, this can be used directly at the type level:

```mojo
comptime result = BlockedProductLayout[type_of(block), type_of(tiler)]()
comptime coalesced = BlockedProductLayout[
    type_of(block), type_of(tiler), coalesce_output=True
]()
```

Parameters:
    BlockLayoutType: The inner block layout type.
    TilerLayoutType: The outer tiler layout type.
    coalesce_output: Whether to coalesce contiguous modes. Default is False.
"""


comptime _can_coalesce_mode[
    block_shape: Int, block_stride: Int, outer_stride: Int
] = block_shape * block_stride == outer_stride
"""Check if a blocked-product mode can be coalesced.

A mode can be coalesced when the inner (block) elements are contiguous
with the outer (tiler) elements, i.e., ``block_shape * block_stride ==
outer_stride``.

Args:
    block_shape: The block shape for this mode.
    block_stride: The block stride for this mode.
    outer_stride: The outer stride (``block.cosize * tiler.stride``)
        for this mode.

Returns:
    True if the mode can be merged into a single flat dimension.
"""


comptime _CoalescedBlockedShapeReducer[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
    block_cosize: Int,
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    # Coalesce: merge into flat ComptimeInt[block_s * tiler_s].
    Variadic.types[
        T=CoordLike,
        ComptimeInt[
            BlockLayoutType._shape_types[idx].static_value
            * TilerLayoutType._shape_types[idx].static_value
        ],
    ] if _can_coalesce_mode[
        BlockLayoutType._shape_types[idx].static_value,
        BlockLayoutType._stride_types[idx].static_value,
        block_cosize * TilerLayoutType._stride_types[idx].static_value,
    ] else
    # No coalesce: keep nested Coord[(block_s, tiler_s)].
    Variadic.types[
        T=CoordLike,
        Coord[
            BlockLayoutType._shape_types[idx],
            TilerLayoutType._shape_types[idx],
        ],
    ],
]


comptime _CoalescedBlockedStrideReducer[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
    block_cosize: Int,
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    # Coalesce: stride is the inner (block) stride.
    Variadic.types[
        T=CoordLike,
        BlockLayoutType._stride_types[idx],
    ] if _can_coalesce_mode[
        BlockLayoutType._shape_types[idx].static_value,
        BlockLayoutType._stride_types[idx].static_value,
        block_cosize * TilerLayoutType._stride_types[idx].static_value,
    ] else
    # No coalesce: keep nested Coord[(block_d, outer_d)].
    Variadic.types[
        T=CoordLike,
        Coord[
            BlockLayoutType._stride_types[idx],
            ComptimeInt[
                block_cosize * TilerLayoutType._stride_types[idx].static_value
            ],
        ],
    ],
]


def blocked_product[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
    //,
](block: BlockLayoutType, tiler: TilerLayoutType) -> BlockedProductLayout[
    BlockLayoutType, TilerLayoutType
]:
    """Creates a blocked layout by combining a block and tiler layout.

    This function creates a hierarchical blocked layout where each element
    of the tiler layout is replaced by a block. This is useful for creating
    tiled layouts for efficient cache utilization.

    Parameters:
        BlockLayoutType: The type of the block layout.
        TilerLayoutType: The type of the tiler layout.

    Args:
        block: The inner layout defining the structure of each tile.
        tiler: The outer layout defining the arrangement of tiles.

    Returns:
        A new layout representing the blocked structure.

    Example:

    ```mojo
    from layout.tile_layout import row_major, blocked_product

    # Create a 2x2 block layout
    var block = row_major[2, 2]()
    # Create a 2x3 tiler (2 rows, 3 cols of blocks)
    var tiler = row_major[2, 3]()
    # Create blocked layout
    var blocked = blocked_product(block, tiler)
    # Result: shape ((2,2), (2,3)), stride ((2,12), (1,4))
    ```
    """
    comptime BlockShape = Coord[*BlockLayoutType._shape_types]
    comptime OuterStrideTypes = _MultiplyByScalar[
        TilerLayoutType._stride_types,
        BlockShape.static_product,
    ]

    # Build inner shape/stride from block layout
    var inner_shape = block.shape_coord()
    var inner_stride = block.stride_coord()

    # Build outer shape from tiler layout
    var outer_shape = tiler.shape_coord()

    # Build outer stride = block.cosize * tiler.stride
    var outer_stride = Coord[*OuterStrideTypes]()

    comptime for i in range(outer_shape.rank):
        comptime if OuterStrideTypes[i].is_static_value:
            UnsafePointer(to=outer_stride[i]).init_pointee_copy(
                rebind[OuterStrideTypes[i]](
                    ComptimeInt[OuterStrideTypes[i].static_value]()
                )
            )
        else:
            var block_cosize = block.shape_coord().product()
            UnsafePointer(to=outer_stride[i]).init_pointee_copy(
                rebind[OuterStrideTypes[i]](
                    RuntimeInt[OuterStrideTypes[i].DTYPE](
                        Scalar[OuterStrideTypes[i].DTYPE](
                            tiler.stride_coord()[i].value() * block_cosize
                        )
                    )
                )
            )

    # Zip per dimension: mode i = (block[i], tiler[i])
    comptime ResultType = BlockedProductLayout[BlockLayoutType, TilerLayoutType]
    var result_shape = Coord[*ResultType._shape_types]()
    var result_stride = Coord[*ResultType._stride_types]()

    comptime for i in range(inner_shape.rank):
        UnsafePointer(to=result_shape[i]).init_pointee_copy(
            rebind[ResultType._shape_types[i]](
                Coord(inner_shape[i], outer_shape[i])
            )
        )
        UnsafePointer(to=result_stride[i]).init_pointee_copy(
            rebind[ResultType._stride_types[i]](
                Coord(inner_stride[i], outer_stride[i])
            )
        )

    return Layout(result_shape, result_stride)


def blocked_product[
    BlockLayoutType: TensorLayout,
    TilerLayoutType: TensorLayout,
    //,
    *,
    coalesce_output: Bool,
](block: BlockLayoutType, tiler: TilerLayoutType) -> BlockedProductLayout[
    BlockLayoutType, TilerLayoutType, coalesce_output
]:
    """Creates a blocked layout with optional output coalescing.

    This overload accepts a ``coalesce_output`` keyword parameter.  When
    True, contiguous inner/outer dimension pairs are merged into flat
    dimensions, reducing the layout rank where possible.

    Parameters:
        BlockLayoutType: The type of the block layout.
        TilerLayoutType: The type of the tiler layout.
        coalesce_output: When True, merge contiguous inner/outer pairs.

    Args:
        block: The inner layout defining the structure of each tile.
        tiler: The outer layout defining the arrangement of tiles.

    Returns:
        A new layout representing the blocked structure, coalesced if
        requested.

    Example:

    ```mojo
    from layout.tile_layout import row_major, blocked_product

    var block = row_major[4]()
    var tiler = row_major[3]()
    # Coalesced: shape (12,), stride (1,) instead of ((4,), (3,))
    var coalesced = blocked_product[coalesce_output=True](block, tiler)
    ```
    """
    return BlockedProductLayout[
        BlockLayoutType, TilerLayoutType, coalesce_output
    ]()


# ===----------------------------------------------------------------------=== #
# Upcast / Downcast
# ===----------------------------------------------------------------------=== #


def _comptime_shape_div(a: Int, b: Int) -> Int:
    """Compile-time shape_div: ``a // b`` if divisible, else ``signum(a * b)``.

    This mirrors the int-int case of the legacy ``shape_div`` function.
    Used by the upcast type-level reducers to compute result types.

    Args:
        a: The dividend.
        b: The divisor.

    Returns:
        ``a // b`` when ``a`` is evenly divisible by ``b``, otherwise
        1 if ``a * b > 0`` else -1.
    """
    if a % b == 0:
        return a // b
    return 1 if a * b > 0 else -1


comptime _UpcastStrideReducer[
    factor: Int,
    stride_types: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    Variadic.types[
        T=CoordLike,
        ComptimeInt[
            _comptime_shape_div(stride_types[idx].static_value, factor)
        ],
    ] if stride_types[idx].is_static_value else Variadic.types[
        T=CoordLike, RuntimeInt[stride_types[idx].DTYPE]
    ],
]
"""Computes the type for each upcast stride dimension.

For a compile-time stride ``d``, the result type is
``ComptimeInt[shape_div(d, factor)]``. For a runtime stride, the result
is ``RuntimeInt``.
"""


comptime _UpcastStrideTypes[
    factor: Int,
    stride_types: Variadic.TypesOfTrait[CoordLike],
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=stride_types,
    Reducer=_UpcastStrideReducer[factor, stride_types, ...],
]
"""The stride types after upcast by ``factor``."""


comptime _UpcastShapeReducer[
    factor: Int,
    shape_types: Variadic.TypesOfTrait[CoordLike],
    stride_types: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    Variadic.types[
        T=CoordLike,
        ComptimeInt[
            _comptime_shape_div(
                shape_types[idx].static_value,
                _comptime_shape_div(factor, stride_types[idx].static_value),
            )
        ],
    ] if shape_types[idx].is_static_value
    and stride_types[idx].is_static_value else Variadic.types[
        T=CoordLike, RuntimeInt[shape_types[idx].DTYPE]
    ],
]
"""Computes the type for each upcast shape dimension.

For compile-time shape ``s`` and stride ``d``, the result type is
``ComptimeInt[shape_div(s, shape_div(factor, d))]``. When either is
runtime, the result is ``RuntimeInt``.
"""


comptime _UpcastShapeTypes[
    factor: Int,
    shape_types: Variadic.TypesOfTrait[CoordLike],
    stride_types: Variadic.TypesOfTrait[CoordLike],
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=shape_types,
    Reducer=_UpcastShapeReducer[factor, shape_types, stride_types, ...],
]
"""The shape types after upcast by ``factor``."""


comptime UpcastLayout[
    LayoutType: TensorLayout,
    factor: Int,
] = Layout[
    _UpcastShapeTypes[
        factor, LayoutType._shape_types, LayoutType._stride_types
    ],
    _UpcastStrideTypes[factor, LayoutType._stride_types],
]
"""Type alias for the result of `upcast`.

Fuses ``factor`` consecutive elements per dimension, producing a layout
with coarser granularity. For fully-static layouts, this can be used
directly at the type level without calling `upcast`:

```mojo
comptime result = UpcastLayout[type_of(my_layout), 2]()
```

Parameters:
    LayoutType: The input layout type.
    factor: The number of consecutive elements to fuse.
"""


@always_inline("nodebug")
def _runtime_shape_div(a: Int, b: Int) -> Int:
    """Runtime shape_div: ``a // b`` if divisible, else ``signum(a * b)``.

    Runtime counterpart of ``_comptime_shape_div``, used in the ``upcast``
    function body for dimensions whose values are only known at runtime.

    Args:
        a: The dividend.
        b: The divisor.

    Returns:
        ``a // b`` when ``a`` is evenly divisible by ``b``, otherwise
        1 if ``a * b > 0`` else -1.
    """
    if a % b == 0:
        return a // b
    return 1 if a * b > 0 else -1


def upcast[
    LayoutType: TensorLayout, factor: Int, //
](layout: LayoutType) -> UpcastLayout[LayoutType, factor]:
    """Fuses consecutive elements in a layout to create a coarser layout.

    This is useful for converting between different data type granularities.
    For example, if a layout describes byte-level offsets and you want to
    treat every 2 bytes as one ``bf16`` element, use ``upcast[2](layout)``.

    For each dimension with shape ``s`` and stride ``d``:
    - ``new_stride = shape_div(d, factor)``
    - ``new_shape  = shape_div(s, shape_div(factor, d))``

    where ``shape_div(a, b)`` returns ``a // b`` if ``a`` is divisible by
    ``b``, otherwise ``signum(a * b)`` (i.e., 1 for positive values).

    Parameters:
        LayoutType: The type of the input layout.
        factor: The number of consecutive elements to fuse into one.

    Args:
        layout: The layout to upcast.

    Returns:
        A new layout with adjusted shape and stride for the coarser
        granularity.

    Example:

    ```mojo
    from layout.tile_layout import row_major, upcast

    # 4x8 row-major, strides (8, 1)
    var layout = row_major[4, 8]()
    # Upcast by 2: treat pairs as single elements
    var coarser = upcast[factor=2](layout)
    # Result: shape (4, 4), strides (4, 1)
    ```
    """
    comptime ResultType = UpcastLayout[LayoutType, factor]
    comptime ResultShapeTypes = ResultType._shape_types
    comptime ResultStrideTypes = ResultType._stride_types

    var new_shape = Coord[*ResultShapeTypes]()
    var new_stride = Coord[*ResultStrideTypes]()

    comptime for i in range(LayoutType.rank):
        comptime s_static = LayoutType._shape_types[i].static_value
        comptime d_static = LayoutType._stride_types[i].static_value

        # Compute new_stride[i] = shape_div(stride[i], factor).
        comptime if ResultStrideTypes[i].is_static_value:
            UnsafePointer(to=new_stride[i]).init_pointee_copy(
                ResultStrideTypes[i]()
            )
        else:
            UnsafePointer(to=new_stride[i]).init_pointee_copy(
                rebind[ResultStrideTypes[i]](
                    RuntimeInt(
                        Scalar[ResultStrideTypes[i].DTYPE](
                            _runtime_shape_div(
                                layout.stride_coord()[i].value(), factor
                            )
                        )
                    )
                )
            )

        # Compute new_shape[i] = shape_div(shape[i], shape_div(factor, stride[i])).
        comptime if ResultShapeTypes[i].is_static_value:
            UnsafePointer(to=new_shape[i]).init_pointee_copy(
                ResultShapeTypes[i]()
            )
        else:
            UnsafePointer(to=new_shape[i]).init_pointee_copy(
                rebind[ResultShapeTypes[i]](
                    RuntimeInt(
                        Scalar[ResultShapeTypes[i].DTYPE](
                            _runtime_shape_div(
                                layout.shape_coord()[i].value(),
                                _runtime_shape_div(
                                    factor,
                                    layout.stride_coord()[i].value(),
                                ),
                            )
                        )
                    )
                )
            )

    return Layout(new_shape, new_stride)


# ===----------------------------------------------------------------------=== #
# Coalesce
# ===----------------------------------------------------------------------=== #


comptime _DropLast2Reducer[
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    Variadic.types[T=CoordLike, From[idx]],
] if idx < Variadic.size(
    From
) - 2 else Prev
"""Keeps all elements except the last two."""


comptime _DropLast2[
    types: Variadic.TypesOfTrait[CoordLike],
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=types,
    Reducer=_DropLast2Reducer,
]
"""Remove the last two elements from a variadic."""


comptime _CoalesceReducer[
    flat_shape_types: Variadic.TypesOfTrait[CoordLike],
    flat_stride_types: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Prev if flat_shape_types[idx].static_value == 1 else (
    # prev_shape == 1: replace last pair with current (shape, stride)
    Variadic.concat_types[
        _DropLast2[Prev],
        Variadic.types[
            T=CoordLike,
            ComptimeInt[flat_shape_types[idx].static_value],
            ComptimeInt[flat_stride_types[idx].static_value],
        ],
    ] if Prev[Variadic.size(Prev) - 2].static_value
    == 1 else (
        # Contiguous: merge into previous (prev_shape * cur_shape, prev_stride)
        Variadic.concat_types[
            _DropLast2[Prev],
            Variadic.types[
                T=CoordLike,
                ComptimeInt[
                    Prev[Variadic.size(Prev) - 2].static_value
                    * flat_shape_types[idx].static_value
                ],
                Prev[Variadic.size(Prev) - 1],
            ],
        ] if Prev[Variadic.size(Prev) - 2].static_value
        * Prev[Variadic.size(Prev) - 1].static_value
        == flat_stride_types[idx].static_value else
        # Non-contiguous: append new (shape, stride) pair
        Variadic.concat_types[
            Prev,
            Variadic.types[
                T=CoordLike,
                ComptimeInt[flat_shape_types[idx].static_value],
                ComptimeInt[flat_stride_types[idx].static_value],
            ],
        ]
    )
)
"""Reducer for coalescing a flattened layout.

Accumulates interleaved (shape, stride) pairs in ``Prev``.  At each step
the current dimension is either skipped (shape == 1), merged into the
previous pair (contiguous strides), or appended as a new pair.

Parameters:
    flat_shape_types: Flattened shape types of the input layout.
    flat_stride_types: Flattened stride types of the input layout.
    Prev: Accumulated interleaved (shape, stride) pairs so far.
    From: The variadic being iterated (same as ``flat_shape_types``).
    idx: Current dimension index.
"""


comptime _CoalescedInterleaved[
    shape_types: Variadic.TypesOfTrait[CoordLike],
    stride_types: Variadic.TypesOfTrait[CoordLike],
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.types[T=CoordLike, ComptimeInt[1], ComptimeInt[0]],
    VariadicType=_Flattened[*shape_types],
    Reducer=_CoalesceReducer[
        _Flattened[*shape_types],
        _Flattened[*stride_types],
        ...,
    ],
]
"""Interleaved (shape0, stride0, shape1, stride1, ...) after coalescing."""


comptime _HalfSizeDriver[
    N: Int,
] = Variadic.splat_type[Trait=CoordLike, count=N // 2, type=ComptimeInt[0]]
"""A dummy variadic of size N//2 used to drive even/odd extraction."""


comptime _ExtractEvenReducer[
    interleaved: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    Variadic.types[T=CoordLike, interleaved[idx * 2]],
]
"""Extracts even-indexed elements from interleaved given as parameter."""


comptime _ExtractOddReducer[
    interleaved: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.TypesOfTrait[CoordLike],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.concat_types[
    Prev,
    Variadic.types[T=CoordLike, interleaved[idx * 2 + 1]],
]
"""Extracts odd-indexed elements from interleaved given as parameter."""


comptime _CoalescedShapeTypes[
    shape_types: Variadic.TypesOfTrait[CoordLike],
    stride_types: Variadic.TypesOfTrait[CoordLike],
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=_HalfSizeDriver[
        Variadic.size(_CoalescedInterleaved[shape_types, stride_types])
    ],
    Reducer=_ExtractEvenReducer[
        _CoalescedInterleaved[shape_types, stride_types], ...
    ],
]
"""Coalesced shape types extracted from the interleaved result."""


comptime _CoalescedStrideTypes[
    shape_types: Variadic.TypesOfTrait[CoordLike],
    stride_types: Variadic.TypesOfTrait[CoordLike],
] = _ReduceVariadicAndIdxToVariadic[
    BaseVal=Variadic.empty_of_trait[CoordLike],
    VariadicType=_HalfSizeDriver[
        Variadic.size(_CoalescedInterleaved[shape_types, stride_types])
    ],
    Reducer=_ExtractOddReducer[
        _CoalescedInterleaved[shape_types, stride_types], ...
    ],
]
"""Coalesced stride types extracted from the interleaved result."""


comptime CoalesceLayout[
    LayoutType: TensorLayout,
] = Layout[
    _CoalescedShapeTypes[LayoutType._shape_types, LayoutType._stride_types],
    _CoalescedStrideTypes[LayoutType._shape_types, LayoutType._stride_types],
]
"""Type alias for the result of `coalesce`.

Simplifies a layout by merging dimensions with contiguous strides.
Adjacent flattened dimensions where ``prev_shape * prev_stride ==
current_stride`` are combined into a single dimension.  Shape-1
dimensions are dropped.

For fully-static layouts, this can be used directly at the type level:

```mojo
comptime result = CoalesceLayout[type_of(my_layout)]()
```

Parameters:
    LayoutType: The input layout type (must have all dimensions known
        at compile time).
"""


@always_inline("nodebug")
def coalesce[
    LayoutType: TensorLayout,
    //,
](layout: LayoutType) -> CoalesceLayout[LayoutType]:
    """Simplifies a layout by merging contiguous dimensions.

    Iterates over the flattened (shape, stride) pairs and:

    1. Skips shape-1 dimensions.
    2. Merges a dimension into the previous one when
       ``prev_shape * prev_stride == current_stride`` (contiguous).
    3. Otherwise starts a new dimension.

    The result is the simplest layout that maps coordinates to the same
    linear offsets as the original.

    Parameters:
        LayoutType: The type of the input layout.

    Args:
        layout: The layout to coalesce.

    Returns:
        A new layout with contiguous dimensions merged.

    Example:

    ```mojo
    from layout.tile_layout import Layout, coalesce, row_major, Idx

    # A row-major 2x4 layout has contiguous strides -> coalesces to 1D
    var layout = row_major[2, 4]()
    var coalesced = coalesce(layout)  # shape (8,), stride (1,)
    ```
    """
    return CoalesceLayout[LayoutType]()


# ===--- Weakly Compatible ---=== #
# Checks structural compatibility between two CoordLike types up to 4 levels
# of nesting.  A scalar coord is always compatible.  A tuple coord requires
# the other type to be a tuple of the same length, with all sub-elements
# recursively compatible.
#
# Because reducers cannot recurse, the check is manually unrolled into four
# depth layers (_WCPair3 → _WCPair2 → _WCPair1 → top-level), each using
# a dedicated reducer to AND-accumulate pair checks over element pairs.
# Returns ``True`` (compatible) or ``False`` (incompatible) as a
# compile-time Bool.


comptime _WCPair3[L: CoordLike, C: CoordLike] = (
    True if not C.is_tuple else (
        False if not L.is_tuple else (
            Variadic.size(L.VariadicType) == Variadic.size(C.VariadicType)
        )
    )
)
"""Depth-3 pair check (innermost): scalar coords pass, tuple coords only
check length match without descending further."""


comptime _WCReducer3[
    layout_types: Variadic.TypesOfTrait[CoordLike],
    coord_types: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.ValuesOfType[Bool],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.values[Prev[0] and _WCPair3[layout_types[idx], coord_types[idx]]]
"""AND-accumulates depth-3 pair checks over element pairs."""


comptime _WCPair2[L: CoordLike, C: CoordLike] = (
    True if not C.is_tuple else (
        False if not L.is_tuple else (
            False if Variadic.size(L.VariadicType)
            != Variadic.size(C.VariadicType) else _ReduceVariadicAndIdxToValue[
                BaseVal=Variadic.values[True],
                VariadicType=C.VariadicType,
                Reducer=_WCReducer3[L.VariadicType, C.VariadicType, ...],
            ][0]
        )
    )
)
"""Depth-2 pair check: delegates sub-element checks to depth-3."""


comptime _WCReducer2[
    layout_types: Variadic.TypesOfTrait[CoordLike],
    coord_types: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.ValuesOfType[Bool],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.values[Prev[0] and _WCPair2[layout_types[idx], coord_types[idx]]]
"""AND-accumulates depth-2 pair checks over element pairs."""


comptime _WCPair1[L: CoordLike, C: CoordLike] = (
    True if not C.is_tuple else (
        False if not L.is_tuple else (
            False if Variadic.size(L.VariadicType)
            != Variadic.size(C.VariadicType) else _ReduceVariadicAndIdxToValue[
                BaseVal=Variadic.values[True],
                VariadicType=C.VariadicType,
                Reducer=_WCReducer2[L.VariadicType, C.VariadicType, ...],
            ][0]
        )
    )
)
"""Depth-1 pair check: delegates sub-element checks to depth-2."""


comptime _WCReducer1[
    layout_types: Variadic.TypesOfTrait[CoordLike],
    coord_types: Variadic.TypesOfTrait[CoordLike],
    Prev: Variadic.ValuesOfType[Bool],
    From: Variadic.TypesOfTrait[CoordLike],
    idx: Int,
] = Variadic.values[Prev[0] and _WCPair1[layout_types[idx], coord_types[idx]]]
"""AND-accumulates depth-1 pair checks over element pairs."""


comptime _WeaklyCompatible[
    layout_types: Variadic.TypesOfTrait[CoordLike],
    coord_types: Variadic.TypesOfTrait[CoordLike],
] = (
    False if Variadic.size(layout_types)
    != Variadic.size(coord_types) else _ReduceVariadicAndIdxToValue[
        BaseVal=Variadic.values[True],
        VariadicType=coord_types,
        Reducer=_WCReducer1[layout_types, coord_types, ...],
    ][0]
)
"""Top-level variadic pair check (depth 0): checks that both variadics have
the same length and all element pairs are weakly compatible."""


comptime WeaklyCompatible[
    L: TensorLayout,
    C: Variadic.TypesOfTrait[CoordLike],
] = _WeaklyCompatible[L._shape_types, C]
"""Check structural compatibility between a layout's shape and coordinate types.

Returns ``True`` if compatible, ``False`` otherwise.  A scalar coordinate
element is always compatible.  A tuple coordinate element requires the
corresponding layout shape element to also be a tuple of the same length,
with all sub-elements recursively compatible.  Handles up to 4 levels of
nesting; beyond that, only length equality is verified.

Parameters:
    L: The layout type whose shape structure is checked.
    C: The coordinate element types to check against.
"""
