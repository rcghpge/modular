# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

from std.algorithm.functional import elementwise
from std.gpu.host import DeviceContext
from std.random import NormalRandom
from tensor._indexing import _dot_prod

from std.utils import IndexList


def random_normal[
    dtype: DType,
    rank: Int,
    //,
    output_fn: def[width: SIMDSize, _rank: Int](
        idx: IndexList[_rank], val: SIMD[dtype, width]
    ) capturing[_],
    target: StaticString,
](
    shape: IndexList[rank],
    mean: Float32,
    stddev: Float32,
    seed_ptr: UnsafePointer[Scalar[DType.uint64], ImmutAnyOrigin],
    ctx: DeviceContext,
) raises:
    """Call `output_fn` with values from a normal distribution, matching
    PyTorch CUDA's `torch.randn` element-to-counter mapping.

    For element `i`, mirrors PyTorch's per-thread Philox state:

        thread_id     = i mod GRID_BLOCK
        within_thread = i div GRID_BLOCK   (0..3)

    where `GRID_BLOCK = 256 * min(num_SMs * blocks_per_sm, ceil(numel/256))`.

    A single Philox step at counter `(0, 0, thread_id, 0)` produces 4 normals
    via :func:`std.random.NormalRandom.step_normal_4`; the within_thread
    index selects which lane to write to `output[i]`.

    Bit-exact for `numel <= 4 * GRID_BLOCK_max` (≈ 1.2M elements on B200).

    Parameters:
        dtype: The data type to generate.
        rank: The rank of the underlying buffer.
        output_fn: The function which stores the generated values.
        target: The target to run on.

    Args:
        shape: The shape of the output being stored into by output_fn.
        mean: The mean of the normal distribution.
        stddev: The standard deviation of the normal distribution.
        seed_ptr: Pointer to a single uint64 in device memory containing
            the Philox seed.
        ctx: The device context.
    """

    if stddev <= 0:
        raise Error("stddev must be positive")

    var numel = shape.flattened_length()
    if numel == 0:
        return

    var strides = shape.get_row_major_strides()

    comptime BLOCK_SIZE: Int = 256

    # GRID_BLOCK mirrors PyTorch CUDA's calc_execution_policy. On GPU it
    # depends on the device's SM count (comptime via default_device_info).
    # On CPU we treat the whole tensor as one "thread group", which collapses
    # to within_thread = 0 for every element.
    var grid_block: Int

    comptime if target == "gpu":
        comptime info = DeviceContext.default_device_info
        comptime MAX_GRID = (
            info.sm_count * (info.threads_per_multiprocessor // BLOCK_SIZE)
        )
        var nblocks = (numel + BLOCK_SIZE - 1) // BLOCK_SIZE
        var grid_x = MAX_GRID if nblocks > MAX_GRID else nblocks
        grid_block = grid_x * BLOCK_SIZE
    else:
        grid_block = numel

    @parameter
    @always_inline
    @__copy_capture(strides, seed_ptr, grid_block)
    def generate[
        width: Int, _rank: Int, alignment: Int = 1
    ](idx: IndexList[_rank]):
        comptime assert (
            width == 1
        ), "PyTorch-compat normal kernel uses scalar lanes"
        var i = _dot_prod(rebind[type_of(strides)](idx), strides)
        var thread_id = UInt64(i % grid_block)
        var within_thread = i // grid_block

        var rng = NormalRandom(seed=seed_ptr[0], subsequence=thread_id)
        var four = rng.step_normal_4(mean=mean, stddev=stddev)
        var value = four[within_thread].cast[dtype]()
        output_fn[width=1](idx, SIMD[dtype, 1](value))

    elementwise[generate, simd_width=1, target=target](shape, ctx)
