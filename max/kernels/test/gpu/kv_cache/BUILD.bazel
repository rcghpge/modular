load("//bazel:api.bzl", "mojo_test")

package(default_visibility = ["//max:consumers"])

_GPU_MEMORY = {
    "test_batch_kv_cache_flash_attention_causal_mask_ragged.mojo": "8",
}

_EXTRA_CONSTRAINTS = {
    "test_batch_kv_cache_flash_attention.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_kv_cache_matmul.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_kv_cache_ragged_matmul.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_batch_kv_cache_flash_attention_causal_mask.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_kv_cache_ragged_matmul_scale.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: KERN-2318
    "test_kv_cache_radd.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3187
    "test_batch_kv_cache_flash_attention_causal_mask_ragged_paged.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3187
    "test_kv_cache_store_ragged.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3187
    "test_kv_cache_2m_iadd.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3187
}

[
    mojo_test(
        name = src + ".test",
        size = "large",
        srcs = [
            src,
            "kv_cache_test_utils.mojo",
        ],
        # KERN-2339: Several tests disabled due to bounds check failures
        enable_assertions = select({
            "//conditions:default": src not in [
                "test_kv_cache_matmul.mojo",  # MSTDL-2240: stack frame size exceeds limit with assertions
                "test_kv_cache_ragged_matmul.mojo",  # TODO: MSTDL-1147 understand why this test fails with asserts turned on.
                "test_mha_mixed_ce_tg.mojo",  # KERN-2339: accessing idx=4 in dimension of size 4
                "test_batch_kv_cache_flash_attention_causal_mask_ragged.mojo",  # KERN-2339: bounds check reveals flash attention kernel bug
                "test_batch_kv_cache_flash_attention_causal_mask_ragged_paged.mojo",  # KERN-2339: bounds check reveals flash attention kernel bug
                "test_batch_kv_cache_flash_attention.mojo",  # KERN-2339: bounds check reveals flash attention kernel bug
                "test_batch_kv_cache_flash_attention_causal_mask.mojo",  # KERN-2339: bounds check reveals flash attention kernel bug
            ],
        }),
        exec_properties = {
            "test.resources:gpu-memory": _GPU_MEMORY.get(src, "5"),
        },
        main = src,
        tags = ["gpu"],
        target_compatible_with = ["//:has_gpu"] + _EXTRA_CONSTRAINTS.get(src, []),
        deps = [
            "//max:internal_utils",
            "//max:kv_cache",
            "//max:linalg",
            "//max:nn",
            "//max:quantization",
            "@mojo//:std",
        ],
    )
    for src in glob(
        ["**/*.mojo"],
        exclude = [
            "kv_cache_test_utils.mojo",  # Imported source, not a test
        ],
    )
]
