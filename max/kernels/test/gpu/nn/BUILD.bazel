load("//bazel:api.bzl", "mojo_filecheck_test", "mojo_test")

package(default_visibility = ["//max:consumers"])

_EXTRA_CONSTRAINTS = {
    "test_cudnn_conv.mojo": ["//:nvidia_gpu"],
    "test_conv_transposed_cudnn.mojo": ["//:nvidia_gpu"],
    "test_irfft.mojo": ["//:nvidia_gpu"],
    "test_flash_attention_amd.mojo": ["//:amd_gpu"],
    "test_mha_causal_mask_amd.mojo": ["//:amd_gpu"],  # FIXME: KERN-1448, KERN-1437, KERN-1429
    "test_mha_operand_tma.mojo": select({
        "//:h100_gpu": [],
        "//:b200_gpu": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    "test_mla.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    "test_index_fp8.mojo": select({
        "//:h100_gpu": [],
        "//:b200_gpu": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    "test_score_mod_mha.mojo": ["//:nvidia_gpu"],  # FIXME: KERN-1377
    "test_rms_norm_fused_residual_add.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_topk_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_toppminp_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_argmaxmin_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2397
    "test_gather_nd_oob.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2366
    "test_mha_tile_scheduler.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2405
    "test_argsort.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2366
    "test_mha_mask.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: KERN-2031
    "test_softmax.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_flash_attention.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3049
    "test_padding_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: GEX-2562
    "test_moe_indices.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2489
    "test_topk_gpu_fi.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2673
    "test_layer_norm.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3009
    "test_fused_qk_rope_ragged.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3104
}

_FILECHECK_TESTS = [
    "test_mha_tile_scheduler.mojo",
    "test_padding_gpu.mojo",
    "test_gather.mojo",
    "test_gather_nd_oob.mojo",
]

[
    mojo_filecheck_test(
        name = src + ".test",
        size = "large",
        srcs = [src],
        expect_crash = src == "test_gather_nd_oob.mojo",
        tags = ["gpu"],
        target_compatible_with = ["//:has_gpu"] + _EXTRA_CONSTRAINTS.get(src, []),
        deps = [
            "//max:internal_utils",
            "//max:linalg",
            "//max:nn",
            "//max:testdata",
            "@mojo//:std",
        ],
    )
    for src in _FILECHECK_TESTS
]

[
    mojo_test(
        name = src + ".test",
        size = "large",
        srcs = [src],
        copts = [],
        # KERN-2339: Several tests disabled due to bounds check failures
        enable_assertions = select({
            "//:amd_gpu": False,
            "//conditions:default": src not in [
                "test_layer_norm.mojo",
                "test_topk_gpu_fi.mojo",  # Extreme register pressure, even simplified assertions cause issues
                "test_score_mod_mha.mojo",  # KERN-2339: accessing idx=4 in dimension of size 4
                "test_mha_chunked_causal_mask.mojo",  # KERN-2339: bounds check reveals kernel bug
                "test_mha_causal_mask.mojo",  # KERN-2339: bounds check reveals kernel bug
                "test_mla.mojo",  # KERN-2339: bounds check reveals kernel bug
                "test_flash_attention.mojo",  # KERN-2339: bounds check reveals kernel bug
            ],
        }),
        exec_properties = {
            "test.resources:gpu-memory": "2",
        },
        tags = ["gpu"],
        target_compatible_with = ["//:has_gpu"] + _EXTRA_CONSTRAINTS.get(src, []),
        deps = [
            "//max:internal_utils",
            "//max:linalg",
            "//max:nn",
            "//max:testdata",
            "@mojo//:std",
        ],
    )
    for src in glob(
        ["**/*.mojo"],
        exclude = _FILECHECK_TESTS,
    )
]
