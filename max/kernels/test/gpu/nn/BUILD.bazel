load("//bazel:api.bzl", "mojo_filecheck_test", "mojo_test")

package(default_visibility = ["//max:consumers"])

_EXTRA_CONSTRAINTS = {
    "test_cudnn_conv.mojo": ["//:nvidia_gpu"],
    "test_conv2d_sm100.mojo": select({
        "//:b200_gpu": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    "test_conv_sm100_flux.mojo": select({
        "//:b200_gpu": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    "test_conv_transposed_cudnn.mojo": ["//:nvidia_gpu"],
    "test_irfft.mojo": ["//:nvidia_gpu"],
    "test_flash_attention_amd.mojo": ["//:amd_gpu"],
    "test_mha_causal_mask_amd.mojo": ["//:amd_gpu"],  # FIXME: KERN-1448, KERN-1437, KERN-1429
    "test_mha_operand_tma.mojo": select({
        "//:h100_gpu": [],
        "//:b200_gpu": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    "test_mla.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    "test_mla_decode_paged_variable.mojo": select({
        "//:b200_gpu": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    "test_index_fp8.mojo": select({
        "//:h100_gpu": [],
        "//:b200_gpu": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    "test_score_mod_mha.mojo": ["//:nvidia_gpu"],  # FIXME: KERN-1377
    "test_rms_norm_fused_fp8.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_rms_norm_fused_residual_add.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_topk_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_toppminp_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_argmaxmin_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2397
    "test_gather_nd_oob.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2366
    "test_mha_tile_scheduler.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2405
    "test_argsort.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2366
    "test_mha_mask.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: KERN-2031
    "test_softmax.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
    "test_flash_attention.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//:amd_gpu": ["@platforms//:incompatible"],  # FIXME: MSTDL-2240
        "//conditions:default": [],
    }),  # FIXME: MOCO-3049
    "test_padding_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: GEX-2562
    "test_moe_indices.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2489
    "test_topk_gpu_fi.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2673
    "test_layer_norm.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3009
    "test_fused_qk_rope.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3104
    "test_fused_qk_rope_ragged.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-3104
    # TODO(KERN-2410): Investigate and enable on macOS
    "test_bicubic.mojo": select({
        "@platforms//os:macos": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    "test_rope_ragged.mojo": select({
        "@platforms//os:macos": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    # These tests fail on Apple GPU - needs investigation
    "test_pool_gpu.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    "test_spatial_merge.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    # TODO(GEX-3342): Metal GPU sorting produces incorrect results
    "test_warp_bitonic_sort.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    "test_scatter_set_constant.mojo": select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: PRDT-506
}

_FILECHECK_TESTS = [
    "test_mha_tile_scheduler.mojo",
    "test_padding_gpu.mojo",
    "test_gather.mojo",
    "test_gather_nd_oob.mojo",
]

[
    mojo_filecheck_test(
        name = src + ".test",
        size = "large",
        srcs = [src],
        expect_crash = src == "test_gather_nd_oob.mojo",
        tags = ["gpu"],
        target_compatible_with = ["//:has_gpu"] + _EXTRA_CONSTRAINTS.get(src, []),
        deps = [
            "//max:internal_utils",
            "//max:linalg",
            "//max:nn",
            "//max:testdata",
            "@mojo//:std",
        ],
    )
    for src in _FILECHECK_TESTS
]

[
    mojo_test(
        name = src + ".test",
        size = "large",
        srcs = [src],
        copts = [],
        # KERN-2339: Several tests disabled due to bounds check failures
        enable_assertions = select({
            # On B200, the SM100 MLA path OOB was fixed (kv_row clamping),
            # so test_mla.mojo can run with assertions enabled.
            "//:b200_gpu": src not in [
                "test_mha_chunked_causal_mask.mojo",  # Stack frame size exceeds limit with assertions (e.g. amdgpu)
                "test_flash_attention_amd.mojo",  # MSTDL-2240: stack frame size exceeds limit with assertions
            ],
            # KERN-2339: H100 MLA kernel has OOB in LayoutTensor masking
            # infrastructure on boundary tiles (num_keys not aligned to BN).
            # The H100 path uses a different code path (mla.mojo) where the
            # OOB is in the multistage-MMA / async-copy masked tile handling.
            # The OOB is functionally benign (masked out by _kernel_mask).
            "//conditions:default": src not in [
                "test_mla.mojo",
                "test_mha_chunked_causal_mask.mojo",  # Stack frame size exceeds limit with assertions (e.g. amdgpu)
                "test_flash_attention_amd.mojo",  # MSTDL-2240: stack frame size exceeds limit with assertions
            ],
        }),
        exec_properties = {
            "test.resources:gpu-memory": "3",
        },
        tags = ["gpu"],
        target_compatible_with = ["//:has_gpu"] + _EXTRA_CONSTRAINTS.get(src, []),
        deps = [
            "//max:internal_utils",
            "//max:linalg",
            "//max:nn",
            "//max:testdata",
            "@mojo//:std",
        ],
    )
    for src in glob(
        ["**/*.mojo"],
        exclude = _FILECHECK_TESTS,
    )
]
