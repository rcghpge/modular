# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #


from buffer import Dim, DimList, NDBuffer
from std.gpu.host import DeviceBuffer, DeviceContext
from linalg.matmul import matmul
from layout import TileTensor
from linalg.matmul.gpu import _matmul_gpu
from std.testing import assert_almost_equal

from std.utils import IndexList


def _size[rank: Int](dims: IndexList[rank, ...]) -> Int:
    var size = 1

    comptime for i in range(rank):
        size *= dims[i]
    return size


def _create_device_buffer[
    dtype: DType, rank: Int, shape: DimList
](ctx: DeviceContext, dynamic_shape: IndexList[rank]) raises -> Tuple[
    DeviceBuffer[dtype], NDBuffer[rank=rank, dtype, MutAnyOrigin, shape]
]:
    var storage = ctx.enqueue_create_buffer[dtype](_size(dynamic_shape))
    return (
        storage,
        NDBuffer[rank=rank, dtype, _, shape](
            storage.unsafe_ptr(), dynamic_shape=dynamic_shape
        ),
    )


def _create_host_buffer[
    dtype: DType, rank: Int, shape: DimList
](dynamic_shape: IndexList[rank, ...]) raises -> NDBuffer[
    rank=rank, dtype, MutAnyOrigin, shape
]:
    var storage_ptr = alloc[Scalar[dtype]](_size(dynamic_shape))
    return NDBuffer[rank=rank, dtype, MutAnyOrigin, shape](
        storage_ptr, dynamic_shape=dynamic_shape
    )


def _linspace_fill[
    dtype: DType, rank: Int, shape: DimList
](mut buff: NDBuffer[mut=True, rank=rank, dtype, _, shape]):
    for i in range(buff.size()):
        buff.data[i] = Scalar[dtype](i)


def _create_host_buffer_like[
    dtype: DType, rank: Int, shape: DimList
](buff: NDBuffer[rank=rank, dtype, _, shape]) raises -> NDBuffer[
    rank=rank, dtype, MutAnyOrigin, shape
]:
    return _create_host_buffer[dtype, rank, shape](buff.dynamic_shape)


def _get_test_name[
    dtype: DType, shape_c: DimList, shape_a: DimList, shape_b: DimList
](
    shape_c_dim: IndexList[2],
    shape_a_dim: IndexList[2],
    shape_b_dim: IndexList[2],
) -> String:
    return String(
        "test-case(",
        dtype,
        ") : ",
        shape_c_dim[0],
        (
            "_dynamic"
            + " x "
            + String(shape_b_dim[1]) if shape_c.at[0]().is_dynamic() else " x "
            + String(shape_b_dim[1])
        ),
        (
            "_dynamic"
            + " x "
            + String(shape_a_dim[1]) if shape_b.at[1]().is_dynamic() else " x "
            + String(shape_a_dim[1])
        ),
        "_dynamic" if shape_a.at[1]().is_dynamic() else "",
        ", ... ",
    )


def matmul_test_case[
    dtype: DType,
    shape_c: DimList,
    shape_a: DimList,
    shape_b: DimList,
](
    shape_c_dim: IndexList[2],
    shape_a_dim: IndexList[2],
    shape_b_dim: IndexList[2],
    ctx: DeviceContext,
) raises:
    print(
        _get_test_name[dtype, shape_c, shape_a, shape_b](
            shape_c_dim, shape_a_dim, shape_b_dim
        ),
        end=" ",
    )

    var mat_a_dev = _create_device_buffer[dtype, 2, shape_a](ctx, shape_a_dim)
    var mat_b_dev = _create_device_buffer[dtype, 2, shape_b](ctx, shape_b_dim)
    var mat_a_host = _create_host_buffer_like(mat_a_dev[1])
    var mat_b_host = _create_host_buffer_like(mat_b_dev[1])
    var mat_c_dev = _create_device_buffer[dtype, 2, shape_c](ctx, shape_c_dim)
    var mat_c_host = _create_host_buffer_like(mat_c_dev[1])
    var mat_c_ref_host = _create_host_buffer_like(mat_c_host)

    _linspace_fill(mat_a_host)
    _linspace_fill(mat_b_host)

    ctx.enqueue_copy(mat_a_dev[0], mat_a_host.data)
    ctx.enqueue_copy(mat_b_dev[0], mat_b_host.data)

    _matmul_gpu[use_tensor_core=True](
        TileTensor(mat_c_dev[1]),
        TileTensor(mat_a_dev[1]),
        TileTensor(mat_b_dev[1]),
        ctx,
    )

    ctx.enqueue_copy(mat_c_host.data, mat_c_dev[0])
    ctx.synchronize()

    # FIXME: We should run a reference gpu matmul, the reference should also
    # support applying the epilogue on the final result.
    matmul(
        mat_c_ref_host,
        mat_a_host,
        mat_b_host,
    )

    for m in range(shape_c_dim[0]):
        for n in range(shape_c_dim[1]):
            assert_almost_equal(mat_c_ref_host[m, n], mat_c_host[m, n])

    mat_a_host.data.free()
    mat_b_host.data.free()
    _ = mat_a_dev^
    _ = mat_b_dev^
    _ = mat_c_dev^


struct ValOrDim[dim: Dim = Dim()](Defaultable):
    var value: Int

    def __init__(out self):
        comptime assert (
            not Self.dim.is_dynamic()
        ), "Can't construct a dynamic dim with no runtime value"
        self.value = Self.dim.get()

    def __init__(out self, v: Int):
        self.value = v


def static[d: Int]() -> ValOrDim[d]:
    return ValOrDim[d]()


def dynamic(d: Int) -> ValOrDim[]:
    return ValOrDim(d)


def create_matmul_test_case[
    dtype: DType
](ctx: DeviceContext, m: ValOrDim, n: ValOrDim, k: ValOrDim) raises:
    matmul_test_case[
        DType.float32,
        DimList[m.dim, n.dim](),
        DimList[m.dim, k.dim](),
        DimList[k.dim, n.dim](),
    ]((m.value, n.value), (m.value, k.value), (k.value, n.value), ctx)


def main() raises:
    with DeviceContext() as ctx:
        create_matmul_test_case[DType.float32](
            ctx, dynamic(8), static[8](), static[4]()
        )
        create_matmul_test_case[DType.float32](
            ctx, dynamic(16), static[16](), static[8]()
        )
