load("//bazel:api.bzl", "lit_tests", "mojo_test")

# Special configuration for multimem allreduce test - only runs on SM90+ hardware
mojo_test(
    name = "test_multimem_allreduce.mojo.test",
    size = "large",
    srcs = [
        "test_allreduce.mojo",
        "test_multimem_allreduce.mojo",
    ],
    exec_properties = {
        "test.resources:gpu-memory": "4",
    },
    main = "test_multimem_allreduce.mojo",
    tags = ["gpu"],
    target_compatible_with = select({
        "//:h100_gpu": ["//:has_multi_gpu"],
        "//:b200_gpu": ["//:has_multi_gpu"],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    deps = [
        "@mojo//:internal_utils",
        "@mojo//:kv_cache",
        "@mojo//:linalg",
        "@mojo//:nn",
        "@mojo//:quantization",
        "@mojo//:stdlib",
    ],
)

# Configuration for expert parallellism communication test - the shmem package only supports NVIDIA cards
lit_tests(
    name = "test",
    srcs = ["test_ep_comm.mojo"],
    gpu_constraints = [
        "//:has_gpu",
        "//:h100_gpu",  # TODO: Investigate why this test times out on A10
        "//:has_multi_gpu",
    ],
    mojo_deps = [
        "@mojo//:comm",
        "@mojo//:layout",
        "@mojo//:shmem",
        "@mojo//:stdlib",
    ],
    tags = ["gpu"],
    tools = [
        "//KGEN/tools/mojo",
    ],
)

# Regular tests for all other files
[
    mojo_test(
        name = src + ".test",
        size = "large",
        srcs = [src],
        exec_properties = {
            "test.resources:gpu-memory": "4",
        },
        tags = ["gpu"],
        target_compatible_with = [
            "//:has_gpu",
            "//:has_multi_gpu",
        ],
        deps = [
            "@mojo//:internal_utils",
            "@mojo//:kv_cache",
            "@mojo//:linalg",
            "@mojo//:nn",
            "@mojo//:quantization",
            "@mojo//:stdlib",
        ],
    )
    for src in glob(
        ["**/*.mojo"],
        exclude = [
            "test_multimem_allreduce.mojo",
            "test_ep_comm.mojo",
        ],
    )
]
