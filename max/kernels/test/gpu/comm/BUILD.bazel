load("//bazel:api.bzl", "mojo_test")

package(default_visibility = ["//max:consumers"])

# Special configuration for multimem allreduce test - only runs on SM90+ hardware
mojo_test(
    name = "test_multimem_allreduce.mojo.test",
    size = "large",
    srcs = [
        "comm_test_utils.mojo",
        "test_allreduce.mojo",
        "test_multimem_allreduce.mojo",
    ],
    env = {
        "MODULAR_DEVICE_CONTEXT_MEMORY_MANAGER_ONLY": "false",
        "MODULAR_DEVICE_CONTEXT_MEMORY_MANAGER_SIZE_PERCENT": "80",
    },
    exec_properties = {
        "test.resources:gpu-memory": "4",
    },
    main = "test_multimem_allreduce.mojo",
    tags = ["gpu"],
    target_compatible_with = select({
        "//:h100_gpu": ["//:has_multi_gpu"],
        "//:b200_gpu": ["//:has_multi_gpu"],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
    deps = [
        "//max:internal_utils",
        "//max:kv_cache",
        "//max:linalg",
        "//max:nn",
        "//max:quantization",
        "@mojo//:std",
    ],
)

# Tests requiring higher GPU memory allocation (20GB instead of 4GB)
HIGH_GPU_MEMORY_TESTS = [
    "test_p2p_ep_combine.mojo",
    "test_p2p_ep_dispatch.mojo",
    "test_p2p_ep_dispatch_fp8.mojo",
]

# Regular tests for all other files
[
    mojo_test(
        name = src + ".test",
        size = "large",
        srcs = [
            src,
            "comm_test_utils.mojo",
        ],
        env = {
            "MODULAR_DEVICE_CONTEXT_MEMORY_MANAGER_ONLY": "false",
            "MODULAR_DEVICE_CONTEXT_MEMORY_MANAGER_SIZE_PERCENT": "80",
        },
        exec_properties = {
            "test.resources:gpu-memory": "20" if src in HIGH_GPU_MEMORY_TESTS else "4",
        },
        main = src,
        tags = ["gpu"],
        target_compatible_with = [
            "//:has_gpu",
            "//:has_multi_gpu",
        ],
        deps = [
            "//max:internal_utils",
            "//max:kv_cache",
            "//max:linalg",
            "//max:nn",
            "//max:quantization",
            "//max:shmem",
            "@mojo//:std",
        ],
    )
    for src in glob(
        ["**/*.mojo"],
        exclude = [
            "comm_test_utils.mojo",  # Imported source, not a test
            "test_multimem_allreduce.mojo",
        ],
    )
]
