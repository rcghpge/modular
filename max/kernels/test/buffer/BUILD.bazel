load("//bazel:api.bzl", "mojo_filecheck_test", "mojo_test")

package(default_visibility = ["//max:consumers"])

_FILECHECK_TESTS = [
    "test_dimlist.mojo",
    "test_naive_matmul.mojo",
    "test_ndbuffer.mojo",
    "test_ndbuffer_dynamic_shape.mojo",
    "test_ndbuffer_dynamic_stride.mojo",
    "test_ndbuffer_indexing.mojo",
    "test_partial_load_store.mojo",
    "test_strsv.mojo",
]

# Tests that have compilation time regressions on macOS after LLVM bump
# (commit 04013d58e3). TODO(MOCO-3079): Remove once the compiler regression is fixed.
_MACOS_SLOW_COMPILE_TESTS = [
    "test_partial_load_store.mojo",
]

[
    mojo_filecheck_test(
        name = src + ".test",
        srcs = [src],
        copts = [
            "--debug-level",
            "full",
        ],
        # Skip tests with macOS compilation time regressions
        target_compatible_with = select({
            "@platforms//os:macos": ["@platforms//:incompatible"],
            "//conditions:default": [],
        }) if src in _MACOS_SLOW_COMPILE_TESTS else [],
        deps = [
            "//max:internal_utils",
            "@mojo//:std",
            "@mojo//:test_utils",
        ],
    )
    for src in _FILECHECK_TESTS
]

[
    mojo_test(
        name = src + ".test",
        srcs = [src],
        copts = [
            "--debug-level",
            "full",
        ],
        deps = [
            "//max:internal_utils",
            "@mojo//:std",
            "@mojo//:test_utils",
        ],
    )
    for src in glob(
        ["*.mojo"],
        allow_empty = True,  # No non-FileCheck tests yet
        exclude = _FILECHECK_TESTS,
    )
]
