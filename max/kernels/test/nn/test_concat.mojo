# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #


from layout._coord import Coord, CoordLike, Idx
from layout._layout import TensorLayout, row_major
from layout._tile_tensor import TileTensor
from nn.concat import _concat_parallel, _concat_serial, concat

from utils import Index, IndexList, StaticTuple


fn _tuple_to_list[
    LayoutType: TensorLayout,
    //,
    dtype: DType,
](
    elems: StaticTuple[
        TileTensor[dtype, LayoutType, ImmutAnyOrigin],
        ...,
    ]
) -> List[TileTensor[dtype, LayoutType, ImmutAnyOrigin]]:
    var output = List[TileTensor[dtype, LayoutType, ImmutAnyOrigin]](
        capacity=len(elems)
    )
    for i in range(len(elems)):
        output.append(elems[i].as_immut())
    return output^


def test_concat():
    print("== test_concat")

    comptime dtype = DType.float32
    comptime rank = 4
    comptime concat_axis = 2

    comptime l1 = row_major[2, 2, 1, 2]()
    comptime l2 = row_major[2, 2, 2, 2]()
    comptime l3 = row_major[2, 2, 3, 2]()
    var x1_stack = InlineArray[Scalar[dtype], l1.product()](uninitialized=True)
    var x2_stack = InlineArray[Scalar[dtype], l2.product()](uninitialized=True)
    var x3_stack = InlineArray[Scalar[dtype], l3.product()](uninitialized=True)
    var x1 = TileTensor(x1_stack, l1).fill(0)
    var x2 = TileTensor(x2_stack, l2).fill(1)
    var x3 = TileTensor(x3_stack, l3).fill(2)

    comptime out_layout = row_major[2, 2, 6, 2]()
    var out_stack = InlineArray[Scalar[dtype], out_layout.product()](
        uninitialized=True
    )
    var output = TileTensor(out_stack, out_layout).fill(-1)
    var x1_dyn = x1.make_dynamic[DType.int64]()

    var input_tuple = StaticTuple[
        TileTensor[dtype, x1_dyn.LayoutType, ImmutAnyOrigin],
        3,
    ](
        x1_dyn.as_any_origin().as_immut(),
        x2.make_dynamic[DType.int64]().as_any_origin().as_immut(),
        x3.make_dynamic[DType.int64]().as_any_origin().as_immut(),
    )

    @parameter
    @always_inline
    fn epilogue_plus_one[
        c_type: DType, _rank: Int, width: Int, *, alignment: Int
    ](indices: IndexList[_rank], val: SIMD[c_type, width]):
        var coord = Coord(indices)
        comptime assert coord.flat_rank == output.flat_rank
        output.store[width=width](
            coord,
            rebind[SIMD[dtype, width]](val + 1),
        )

    concat[dtype, False, epilogue_fn=epilogue_plus_one](
        output.make_dynamic[DType.int64](), concat_axis, input_tuple
    )

    # CHECK: == test_concat
    # CHECK-COUNT-2: 1.0
    # CHECK-COUNT-4: 2.0
    # CHECK-COUNT-6: 3.0
    # CHECK-COUNT-2: 1.0
    # CHECK-COUNT-4: 2.0
    # CHECK-COUNT-6: 3.0
    # CHECK-COUNT-2: 1.0
    # CHECK-COUNT-4: 2.0
    # CHECK-COUNT-6: 3.0
    var output_flat = TileTensor(
        output.ptr,
        row_major(Coord(Idx(output.numel()))),
    )
    for i in range(output.layout.product()):
        print(output_flat.load[1]((Idx(i),)))


def test_concat_parallel():
    print("== test_concat_parallel")

    comptime dtype = DType.float32
    comptime rank = 4
    comptime concat_axis = 2

    comptime l1 = row_major[2, 2, 1, 2]()
    comptime l2 = row_major[2, 2, 2, 2]()
    comptime l3 = row_major[2, 2, 3, 2]()
    var x1_stack = InlineArray[Scalar[dtype], l1.product()](uninitialized=True)
    var x2_stack = InlineArray[Scalar[dtype], l2.product()](uninitialized=True)
    var x3_stack = InlineArray[Scalar[dtype], l3.product()](uninitialized=True)
    var x1 = TileTensor(x1_stack, l1).fill(0)
    var x2 = TileTensor(x2_stack, l2).fill(1)
    var x3 = TileTensor(x3_stack, l3).fill(2)

    var x1_dyn = x1.make_dynamic[DType.int64]()
    var x2_dyn = x2.make_dynamic[DType.int64]()
    var x3_dyn = x3.make_dynamic[DType.int64]()

    comptime out_layout = row_major[2, 2, 6, 2]()
    var out_stack = InlineArray[Scalar[dtype], out_layout.product()](
        uninitialized=True
    )
    var output = TileTensor(out_stack, out_layout).fill(-1)

    var input_tuple = StaticTuple[
        TileTensor[dtype, x1_dyn.LayoutType, ImmutAnyOrigin],
        3,
    ](
        x1_dyn.as_any_origin().as_immut(),
        x2_dyn.as_any_origin().as_immut(),
        x3_dyn.as_any_origin().as_immut(),
    )

    @parameter
    @always_inline
    fn epilogue_plus_one[
        c_type: DType, _rank: Int, width: Int, *, alignment: Int
    ](indices: IndexList[_rank], val: SIMD[c_type, width]):
        var coord = Coord(indices)
        comptime assert coord.flat_rank == output.flat_rank
        output.store[width=width](
            coord,
            rebind[SIMD[dtype, width]](val + 1),
        )

    var input_vec = _tuple_to_list(input_tuple)
    _concat_parallel[dtype, epilogue_plus_one](
        output.make_dynamic[DType.int64](), concat_axis, input_vec
    )

    # CHECK: == test_concat_parallel
    # CHECK-COUNT-2: 1.0
    # CHECK-COUNT-4: 2.0
    # CHECK-COUNT-6: 3.0
    # CHECK-COUNT-2: 1.0
    # CHECK-COUNT-4: 2.0
    # CHECK-COUNT-6: 3.0
    # CHECK-COUNT-2: 1.0
    # CHECK-COUNT-4: 2.0
    # CHECK-COUNT-6: 3.0
    var output_flat = TileTensor(
        output.ptr,
        row_major(Coord(Idx(output.numel()))),
    )
    for i in range(output.layout.product()):
        print(output_flat.load[1]((Idx(i),)))


# CHECK-LABEL: test_concat_inner
def test_concat_inner():
    print("== test_concat_inner")

    comptime dtype = DType.float32
    comptime rank = 5
    comptime concat_axis = 2

    comptime l1 = row_major[1, 1, 1, 2, 2]()
    comptime l2 = row_major[1, 1, 2, 2, 2]()
    comptime l3 = row_major[1, 1, 3, 2, 2]()
    var x1_stack = InlineArray[Scalar[dtype], l1.product()](uninitialized=True)
    var x2_stack = InlineArray[Scalar[dtype], l2.product()](uninitialized=True)
    var x3_stack = InlineArray[Scalar[dtype], l3.product()](uninitialized=True)
    var x1 = TileTensor(x1_stack, l1).fill(0)
    var x2 = TileTensor(x2_stack, l2).fill(1)
    var x3 = TileTensor(x3_stack, l3).fill(2)

    var x1_dyn = x1.make_dynamic[DType.int64]()
    var x2_dyn = x2.make_dynamic[DType.int64]()
    var x3_dyn = x3.make_dynamic[DType.int64]()

    comptime out_layout = row_major[1, 1, 6, 2, 2]()
    var out_stack = InlineArray[Scalar[dtype], out_layout.product()](
        uninitialized=True
    )
    var output = TileTensor(out_stack, out_layout).fill(-1)

    var input_tuple = StaticTuple[
        TileTensor[dtype, x1_dyn.LayoutType, ImmutAnyOrigin],
        3,
    ](
        x1_dyn.as_any_origin().as_immut(),
        x2_dyn.as_any_origin().as_immut(),
        x3_dyn.as_any_origin().as_immut(),
    )

    var input_vec = _tuple_to_list(input_tuple)

    @parameter
    @always_inline
    fn epilogue_plus_one[
        c_type: DType, _rank: Int, width: Int, *, alignment: Int
    ](indices: IndexList[_rank], val: SIMD[c_type, width]):
        var coord = Coord(indices)
        comptime assert coord.flat_rank == output.flat_rank
        output.store[width=width](
            coord,
            rebind[SIMD[dtype, width]](val + 1),
        )

    _concat_serial[dtype, epilogue_plus_one](
        output.make_dynamic[DType.int64](), concat_axis, input_vec
    )

    # CHECK-COUNT-4: 1.0
    # CHECK-COUNT-8: 2.0
    # CHECK-COUNT-12: 3.0
    var output_flat = TileTensor(
        output.ptr,
        row_major(Coord(Idx(output.numel()))),
    )
    for i in range(output.layout.product()):
        print(output_flat.load[1]((Idx(i),)))


def main():
    test_concat()
    test_concat_parallel()
    test_concat_inner()
