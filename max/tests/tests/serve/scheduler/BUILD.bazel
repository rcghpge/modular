load("//bazel:api.bzl", "modular_py_library", "modular_py_test", "requirement")

# Normal CPU tests
[
    modular_py_test(
        name = src.split("/")[-1].split(".")[0],
        srcs = [src],
        deps = [":common"],
    )
    for src in glob(
        ["*.py"],
        exclude = [
            "common.py",
            "test_di.py",
            "*_gpu.py",
        ],
    )
]

# CPU test that needs UCX Transfer Engine requires x86_64 linux machines (eg: m7i)
modular_py_test(
    name = "test_di",
    srcs = ["test_di.py"],
    target_compatible_with = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
    ],
    deps = [":common"],
)

# GPU tests
[
    modular_py_test(
        name = src.split("/")[-1].split(".")[0],
        srcs = [src],
        exec_properties = {
            "test.resources:gpu-memory": "1",
        },
        gpu_constraints = [
            "//:has_gpu",
            "//:nvidia_gpu",
        ] + select({
            "//:apple_gpu": ["@platforms//:incompatible"],
            "//conditions:default": [],
        }),
        tags = ["gpu"],
        deps = [":common"],
    )
    for src in glob(
        ["*_gpu.py"],
        exclude = ["common.py"],
    )
]

modular_py_library(
    name = "common",
    srcs = ["common.py"],
    deps = [
        "//SDK/lib/API/python/max/nn",
        "//SDK/lib/API/python/max/serve",
        "//SDK/lib/API/python/max/serve/scheduler",
        requirement("numpy"),
        requirement("pytest-mock"),
        requirement("pytest-asyncio"),
    ],
)
