load("//bazel:api.bzl", "modular_py_library", "modular_py_test", "requirement")

package(default_visibility = [
    "//:__pkg__",
    "//SDK/integration-test:__subpackages__",
    "//max/tests:__subpackages__",
    "//oss/modular/max/tests:__subpackages__",
])

# Normal CPU tests
[
    modular_py_test(
        name = src.split("/")[-1].split(".")[0],
        srcs = [src],
        mojo_deps = ["//max:MOGGKernelAPI"],
        tags = [
            "no-pydeps",  # Declaring tests in a loop like this doesn't work well with pydeps
        ] + (
            ["external-exclusive"] if src in [
                "test_tts_scheduler.py",
                "test_paged_scheduler.py",
            ] else []  # Times out if run with too many other tests
        ),
        deps = [
            ":common",
            requirement("pytest-asyncio"),
            requirement("pytest-mock"),
        ],
    )
    for src in glob(
        ["*.py"],
        exclude = [
            "common.py",
            "test_di.py",
            "*_gpu.py",
        ],
    )
]

# CPU test that needs UCX Transfer Engine requires x86_64 linux machines (eg: m7i)
modular_py_test(
    name = "test_di",
    srcs = ["test_di.py"],
    mojo_deps = ["//max:MOGGKernelAPI"],
    tags = [
        "external-exclusive",  # Times out if run with too many other tests
    ],
    target_compatible_with = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
    ],
    deps = [
        ":common",
        "//max/python/max/driver",
        "//max/python/max/interfaces",
        "//max/python/max/kv_cache",
        "//max/python/max/pipelines/core",
        "//max/python/max/serve:config",
        "//max/python/max/serve/scheduler",
        "//max/python/max/serve/worker_interface",
        requirement("numpy"),
    ],
)

# GPU tests
[
    modular_py_test(
        name = src.split("/")[-1].split(".")[0],
        srcs = [src],
        exec_properties = {
            "test.resources:gpu-memory": "1",
        },
        gpu_constraints = [
            "//:has_gpu",
            "//:nvidia_gpu",
        ] + select({
            "//:apple_gpu": ["@platforms//:incompatible"],
            "//conditions:default": [],
        }),
        mojo_deps = ["//max:MOGGKernelAPI"],
        tags = [
            "gpu",
            "no-pydeps",  # Declaring tests in a loop like this doesn't work well with pydeps
        ],
        deps = [":common"],
    )
    for src in glob(
        ["*_gpu.py"],
        exclude = ["common.py"],
    )
]

modular_py_library(
    name = "common",
    srcs = ["common.py"],
    imports = ["../../.."],
    deps = [
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/interfaces",
        "//max/python/max/kv_cache",
        "//max/python/max/nn",
        "//max/python/max/pipelines/core",
        "//max/python/max/serve/scheduler",
        requirement("numpy"),
    ],
)
