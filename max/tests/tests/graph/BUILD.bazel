load("//bazel:api.bzl", "modular_multi_py_version_test", "modular_py_test", "requirement")

package(default_visibility = [
    "//:__pkg__",
    "//SDK/integration-test:__subpackages__",
    "//max/tests:__subpackages__",
    "//oss/modular/max/tests:__subpackages__",
])

_MULTI_VERSION_TEST_SRCS = [
    "test_graph.py",
]

# TODO(GEX-3155): Tests that timeout on macOS
_MACOS_INCOMPATIBLE_TESTS = [
    "ops/test_argsort.py",
    "ops/test_layer_norm.py",
    "ops/elementwise/test_is_nan.py",
]

modular_py_test(
    name = "tests",
    srcs = glob(
        ["**/*.py"],
        exclude = _MULTI_VERSION_TEST_SRCS + _MACOS_INCOMPATIBLE_TESTS,
    ),
    data = [
        "//max/tests/integration:test_user_op",
        "//max/tests/tests/graph/testdata",
    ],
    env = {
        "CUSTOM_OPS_PATH": "$(rootpath //max/tests/integration:test_user_op)",
        "MODULAR_PATH": ".",
        "TESTDATA_DIRECTORY": "max/tests/tests/graph/testdata",
    },
    ignore_extra_deps = [
        # Needed for loading gguf files, doesn't get pulled by default
        requirement("gguf"),
    ],
    imports = ["."],
    mojo_deps = [
        "//max:MOGGKernelAPI",
        "//max/tests/integration:test_user_op",
        "@mojo//:std",
    ],
    deps = [
        "//max/python/max:_core",
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/mlir",
        "//max/python/max/nn",
        requirement("gguf"),
        requirement("hypothesis"),
        requirement("numpy"),
        requirement("torch"),
    ],
)

# Tests that timeout on macOS - run only on non-macOS platforms
modular_py_test(
    name = "macos_incompatible_tests",
    srcs = _MACOS_INCOMPATIBLE_TESTS + ["conftest.py"],
    data = ["//max/tests/tests/graph/testdata"],
    env = {
        "MODULAR_PATH": ".",
        "TESTDATA_DIRECTORY": "max/tests/tests/graph/testdata",
    },
    ignore_extra_deps = [
        # Needed for KernelLibrary initialization (GEX-1846)
        "//max/python/max/driver",
        "//max/python/max/engine",
    ],
    imports = ["."],
    mojo_deps = [
        "//max:MOGGKernelAPI",
        "@mojo//:std",
    ],
    # TODO(GEX-3155): Investigate and enable on macOS
    target_compatible_with = select({
        "@platforms//os:macos": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    deps = [
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/mlir",
        requirement("hypothesis"),
        requirement("numpy"),
    ],
)

modular_multi_py_version_test(
    name = "multi_version_tests",
    srcs = _MULTI_VERSION_TEST_SRCS + [
        "conftest.py",
    ],
    data = ["//max/tests/tests/graph/testdata"],
    env = {"TESTDATA_DIRECTORY": "max/tests/tests/graph/testdata"},
    ignore_extra_deps = [
        # `engine` directly depends on `graph`, this is a circular import
        # needed just for initialization of the graph. This can be removed
        # when GEX-1846 is fixed.
        "//max/python/max/engine",
    ],
    imports = ["."],
    deps = [
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/mlir",
        requirement("hypothesis"),
        requirement("numpy"),
    ],
)
