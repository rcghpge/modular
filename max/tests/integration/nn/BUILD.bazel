load("//bazel:api.bzl", "modular_py_library", "modular_py_test", "requirement")

package(default_visibility = [
    "//:__pkg__",
    "//SDK/integration-test:__subpackages__",
    "//max/tests:__subpackages__",
    "//oss/modular/max/tests:__subpackages__",
])

modular_py_library(
    name = "shared_conv_impl",
    testonly = True,
    srcs = ["shared_conv_impl.py"],
    imports = ["."],
    deps = [
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/nn",
        requirement("torch"),
        requirement("numpy"),
    ],
)

modular_py_library(
    name = "shared_mlp_impl",
    testonly = True,
    srcs = ["shared_mlp_impl.py"],
    imports = ["."],
    deps = [
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/nn",
        "//max/tests/integration/test_common:graph_utils",
        requirement("torch"),
        requirement("pytest"),
    ],
)

[
    modular_py_test(
        name = src.split(".")[0],
        size = "large",
        srcs = [
            src,
            "conftest.py",
        ],
        args = [
            "-n",
            "auto",
        ],
        imports = ["."],
        tags = [
            "no-pydeps",  # Declaring tests in a loop like this doesn't work well with pydeps
        ] + (["skip-external-ci"] if src in [
            # FIXME: SDLC-3032
            "test_conv.py",
            "test_mlp.py",
        ] else []),
        target_compatible_with = select({
            "@platforms//os:macos": ["@platforms//:incompatible"],  # FIXME: MOCO-2411
            "//:asan": ["@platforms//:incompatible"],  # Too slow with ASAN
            "//conditions:default": [],
        }),
        deps = [
            ":shared_conv_impl",
            ":shared_mlp_impl",
            "//max/python/max/engine",
            "//max/python/max/nn",
            "//max/python/max/nn/legacy/hooks",
            "//max/python/max/pipelines",
            "//max/python/max/serve",
            "//max/tests/integration/graph:modular_graph_test",
            "//max/tests/integration/test_common",
            requirement("torch"),
            requirement("transformers"),
            requirement("hypothesis"),
            requirement("gguf"),
            requirement("pytest-mock"),
            requirement("pytest-asyncio"),
            requirement("pytest-xdist"),
        ],
    )
    for src in glob(
        ["*.py"],
        exclude = [
            "conftest.py",
            "*_gpu.py",
            "shared_conv_impl.py",
            "shared_mlp_impl.py",
        ],
    )
]

modular_py_test(
    name = "nn_gpu_tests",
    size = "large",
    srcs = glob(
        ["*_gpu.py"],
        exclude = [
            "*_distributed_gpu.py",
            "test_conv_transpose_gpu.py",
            "test_linear_fp8_amd_gpu.py",
            "test_lora_kernels_gpu.py",
            "test_sgmv_kernels_gpu.py",
        ],
    ) + ["conftest.py"],
    exec_properties = {
        "test.resources:gpu-memory": "3",
    },
    gpu_constraints = ["//:has_gpu"] + select({
        "//:apple_gpu": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),  # FIXME: MOCO-2411
    imports = ["."],
    tags = [
        "gpu",
    ],
    deps = [
        ":shared_conv_impl",
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/nn",
        "//max/tests/integration/test_common:graph_utils",
        "//max/tests/integration/test_common:numerics",
        requirement("numpy"),
        requirement("torch"),
        requirement("triton"),
        requirement("hypothesis"),
    ],
)

modular_py_test(
    name = "nn_nvidia_gpu_tests",
    srcs = [
        "test_conv_transpose_gpu.py",
    ],
    gpu_constraints = ["//:nvidia_gpu"],
    imports = ["."],
    tags = [
        "gpu",
    ],
    deps = [
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/nn",
        "//max/tests/integration/test_common:numerics",
        requirement("numpy"),
        requirement("torch"),
    ],
)

modular_py_test(
    name = "nn_distributed_gpu_tests",
    size = "enormous",
    srcs = glob(["*_distributed_gpu.py"]) + ["conftest.py"],
    exec_properties = {
        "test.resources:gpu-memory": "20",
    },
    gpu_constraints = ["//:has_4_gpus"],
    imports = ["."],
    tags = [
        "gpu",
    ],
    deps = [
        ":shared_mlp_impl",
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/nn",
        requirement("numpy"),
        requirement("torch"),
        requirement("hypothesis"),
    ],
)

modular_py_test(
    name = "amd_tests",
    size = "large",
    srcs = [
        "conftest.py",
        "test_linear_fp8_amd_gpu.py",
    ],
    exec_properties = {
        "test.resources:gpu-memory": "2",
    },
    tags = [
        "gpu",
        # TODO: GEX-2676
        "manual",
    ],
    target_compatible_with = ["//:amd_gpu"],
    deps = [
        "//max/python/max:tensor",
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/nn",
        requirement("hypothesis"),
        requirement("torch"),
    ],
)

modular_py_test(
    name = "sgmv_tests",
    size = "large",
    srcs = [
        "conftest.py",
        "test_sgmv_kernels_gpu.py",
    ],
    exec_properties = {
        "test.resources:gpu-memory": "2",
    },
    tags = [
        "gpu",
    ],
    target_compatible_with = ["//:b200_gpu"],
    deps = [
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/nn",
        requirement("hypothesis"),
        requirement("numpy"),
        requirement("torch"),
    ],
)

modular_py_test(
    name = "lora_tests",
    size = "large",
    srcs = [
        "conftest.py",
        "test_lora_kernels_gpu.py",
    ],
    exec_properties = {
        "test.resources:gpu-memory": "2",
    },
    tags = [
        "gpu",
    ],
    target_compatible_with = ["//:b200_gpu"],
    deps = [
        "//max/python/max/driver",
        "//max/python/max/dtype",
        "//max/python/max/engine",
        "//max/python/max/graph",
        "//max/python/max/nn",
        requirement("hypothesis"),
        requirement("numpy"),
        requirement("torch"),
    ],
)
