"""Define bazel dependencies common to the internal and external repos."""

bazel_dep(name = "abseil-cpp", version = "20250127.1")
bazel_dep(name = "apple_support", version = "1.24.2", repo_name = "build_bazel_apple_support")
bazel_dep(name = "aspect_bazel_lib", version = "2.21.2")
bazel_dep(name = "aspect_rules_js", version = "2.4.1")
bazel_dep(name = "bazel_skylib", version = "1.8.1")
bazel_dep(name = "curl", version = "8.8.0")
bazel_dep(name = "gazelle", version = "0.47.0")
bazel_dep(name = "glfw", version = "3.3.9.bcr.1")
bazel_dep(name = "google_benchmark", version = "1.8.5")
bazel_dep(name = "grpc", version = "1.73.1", repo_name = "com_github_grpc_grpc")
bazel_dep(name = "grpc-java", version = "1.67.1")
bazel_dep(name = "imgui", version = "1.91.8")
bazel_dep(name = "opentelemetry-cpp", version = "1.19.0")
bazel_dep(name = "opentelemetry-proto", version = "1.5.0")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "protobuf", version = "31.1")
bazel_dep(name = "rules_cc", version = "0.2.14")
bazel_dep(name = "rules_go", version = "0.58.3", repo_name = "io_bazel_rules_go")
bazel_dep(name = "rules_java", version = "9.0.3")
bazel_dep(name = "rules_mojo", version = "0.5.0")
bazel_dep(name = "rules_multirun", version = "0.13.0")
bazel_dep(name = "rules_pkg", version = "1.0.1")
bazel_dep(name = "rules_proto", version = "7.0.2")
bazel_dep(name = "rules_pycross", version = "0.8.0")
bazel_dep(name = "rules_python", version = "1.6.3")
bazel_dep(name = "rules_shell", version = "0.6.1")
bazel_dep(name = "with_cfg.bzl", version = "0.10.3")

# TODO: Remove when transitives bump to this version or above
bazel_dep(name = "rules_foreign_cc", version = "0.13.0")  # NOTE: Avoid direct usage
bazel_dep(name = "rules_nodejs", version = "6.5.0")

single_version_override(
    module_name = "abseil-cpp",
    version = "20250127.1",
)

bazel_dep(name = "buildifier_prebuilt", version = "8.2.1", dev_dependency = True)

archive_override(
    module_name = "rules_mojo",
    integrity = "sha256-w7hPwnIRS1yyx0j6nZCD/yGqM0AZiFfJiF9U9DxIr5k=",
    strip_prefix = "rules_mojo-87f860f96f73ce624d51fe8e836978888e14344c",
    urls = [
        "https://github.com/modular/rules_mojo/archive/87f860f96f73ce624d51fe8e836978888e14344c.tar.gz",
    ],
)

single_version_override(
    module_name = "rules_cc",
    patch_strip = 1,
    patches = [
        # https://github.com/bazelbuild/bazel/pull/26925
        "//bazel/public-patches:rules_cc_dsym_feature.patch",
    ],
)

single_version_override(
    module_name = "rules_nodejs",
    patch_strip = 1,
    patches = [
        # https://github.com/bazel-contrib/rules_nodejs/pull/3859
        "//bazel/public-patches:rules_nodejs_toolchains.patch",
    ],
    version = "6.5.0",
)

# Manually pull in https://github.com/bazel-contrib/rules_jvm_external/pull/1265
single_version_override(
    module_name = "rules_jvm_external",
    version = "6.7",
)

bazel_dep(name = "rules_mypy", version = "0.39.0", dev_dependency = True)
single_version_override(
    module_name = "rules_mypy",
    patch_strip = 1,
    patches = [
        # TODO: Should we submit this upstream or force our deps to directly
        # depend on the things that provide their pyi files?
        "//bazel/public-patches:rules_mypy_pyi_files.patch",
        # https://github.com/bazel-contrib/rules_mypy/pull/118
        "//bazel/public-patches:rules_mypy_pyinfo.patch",
    ],
)

single_version_override(
    module_name = "rules_python",
    patch_strip = 1,
    patches = [
        # https://github.com/bazel-contrib/rules_python/pull/3233
        "//bazel/public-patches:rules_python_wheel.patch",
        # https://github.com/bazel-contrib/rules_python/pull/3330
        "//bazel/public-patches:rules_python_3_14.patch",
    ],
)

# TODO: Remove once >0.8.0 is released
archive_override(
    module_name = "rules_pycross",
    integrity = "sha256-8nt5EI2mkzePeqw//u/5jGxxfvEx7727OTIb+tTp+Ag=",
    strip_prefix = "rules_pycross-573feab78c2d559d282d762568bfd88cbb933dfc",
    urls = [
        "https://github.com/jvolkman/rules_pycross/archive/573feab78c2d559d282d762568bfd88cbb933dfc.tar.gz",
    ],
)

single_version_override(
    module_name = "aspect_rules_js",
    patch_strip = 1,
    patches = [
        # https://github.com/aspect-build/rules_js/pull/2330
        "//bazel/public-patches:aspect_rules_js_toolchains.patch",
    ],
)

single_version_override(
    module_name = "opentelemetry-cpp",
    patch_strip = 1,
    patches = [
        "//bazel/public-patches:opentelemetry.patch",
    ],
)

single_version_override(
    module_name = "protobuf",
    patch_strip = 1,
    patches = [
        # TODO: Remove when this patch is in a release
        # https://github.com/protocolbuffers/protobuf/pull/22633
        "//bazel/public-patches:protobuf-platforms.patch",
        # TODO: https://github.com/protocolbuffers/protobuf/commit/a5220488813040037e59b5625f953f2da7bf73a4
        "//bazel/public-patches:protobuf-windows.patch",
        # TODO: https://github.com/protocolbuffers/protobuf/commit/27227205a3986987b7495078aaf7377b49dc38aa
        "//bazel/public-patches:protobuf-debug-context.patch",
        # TODO: https://github.com/protocolbuffers/protobuf/commit/158312dc8c58efc777849dc89734f55c696eae1e remove next bump
        "//bazel/public-patches:protobuf-loads.patch",
        # TODO: https://github.com/protocolbuffers/protobuf/commit/25d8081ab9991bf56d54ad52f0ce529c4e5aa979 remove next bump
        "//bazel/public-patches:protobuf-cc-loads.patch",
    ],
    version = "31.1",  # TODO: Remove restriction next update
)

single_version_override(
    module_name = "with_cfg.bzl",
    patch_strip = 1,
    patches = [
        "//bazel/public-patches:with_cfg_visibility.patch",
    ],
)

single_version_override(
    module_name = "grpc",
    patch_strip = 1,
    patches = [
        # Revert of https://github.com/grpc/grpc//commit/9a6bcd4c2f2913c1bfe8dccf9e536d8f53c360c2
        "//bazel/public-patches:grpc-no-macos-x86.patch",
        # TODO: Drop when logspew is fixed
        "//bazel/public-patches:grpc-warnings.patch",
        # https://github.com/grpc/grpc/pull/40687
        "//bazel/public-patches:grpc-rules-python.patch",
    ],
    version = "1.73.1",
)

single_version_override(
    module_name = "grpc-java",
    patch_strip = 1,
    patches = [
        # TODO: Drop when this is pulled from bzlmod instead of grpc_deps.bzl
        "//bazel/public-patches:grpc-java.patch",
    ],
    version = "1.67.1",
)

DEFAULT_PYTHON_VERSION = "3.12"

PYTHON_VERSIONS = [
    "3_10",
    "3_11",
    "3_12",
    "3_13",
    "3_14",
]

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.defaults(python_version = DEFAULT_PYTHON_VERSION)

[
    python.toolchain(python_version = version.replace("_", "."))
    for version in PYTHON_VERSIONS
]

# Only the lowest version is used explicitly, everything else is indirect
use_repo(python, "python_" + PYTHON_VERSIONS[0])

environments = use_extension("@rules_pycross//pycross/extensions:environments.bzl", "environments")
environments.create_for_python_toolchains(
    name = "rules_pycross_all_environments",
    platforms = [
        "x86_64-unknown-linux-gnu",
        "aarch64-unknown-linux-gnu",
        "aarch64-apple-darwin",
    ],
    python_versions = [x.replace("_", ".") for x in PYTHON_VERSIONS],
)
use_repo(environments, "rules_pycross_all_environments")

lock_file = use_extension("@rules_pycross//pycross/extensions:lock_file.bzl", "lock_file")
lock_file.instantiate(
    name = "modular_pip_lock_file_repo",
    lock_file = "//bazel/pip/requirements:pycross_lock_file.bzl",
)
use_repo(lock_file, "modular_pip_lock_file_repo")

module_versions = use_repo_rule("//bazel/pip:module_versions.bzl", "module_versions")

module_versions(
    name = "module_versions",
    default_python_version = DEFAULT_PYTHON_VERSION,
    python_versions = PYTHON_VERSIONS,
)

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

RUFF_VERSION = "0.14.0"

[
    http_archive(
        name = "ruff-{arch}".format(arch = arch),
        build_file_content = 'exports_files(["ruff"])',
        sha256 = sha,
        strip_prefix = "ruff-{arch}".format(arch = arch),
        url = "https://github.com/astral-sh/ruff/releases/download/{version}/ruff-{arch}.tar.gz".format(
            arch = arch,
            version = RUFF_VERSION,
        ),
    )
    for arch, sha in [
        ("x86_64-unknown-linux-gnu", "28fe06f700caf99eee235f90e6e349f48b7f9a4b0d42e3ee5b3686f9259649a3"),
        ("aarch64-unknown-linux-gnu", "60fc3b464a723456ba1bc8cd91d29806753c885f72e26ac67b718cee6b35aaca"),
        ("aarch64-apple-darwin", "0b7c193d5c45eda02226720eb75239fabeca995d5a0eb3830fd2973caa3030ec"),
    ]
]

http_archive(
    name = "rocshmem",
    build_file_content = """

package(default_visibility = ["//visibility:public"])

# This is the device-side bitcode that is linked into shmem.mojopkg.
filegroup(
    name = "device",
    srcs = ["lib/librocshmem_device.bc"],
)

filegroup(
    name = "host",
    srcs = [
        "bin/mpirun",
        "bin/prterun",
        "lib/libmpi.so",
        "lib/libmpi.so.40.40.7",
        "lib/librocshmem.so",
        "lib/libucp.so.0",
        "lib/libucs.so.0",
        "lib/libopen-pal.so.80",
        "lib/libevent_core-2.1.so.7",
        "lib/libevent_pthreads-2.1.so.7",
        "lib/libhwloc.so.15",
        "lib/libpmix.so.2",
        "lib/libuct.so.0",
        "lib/libucm.so.0",
        "lib/libprrte.so.3",
        "lib/libnl-3.so.200",
        "share/openmpi/help-mpirun.txt",
        "share/prte/help-pcompress.txt",
        "share/pmix/help-pcompress.txt",
    ],
)
""",
    sha256 = "a94edf6e52add4efe8c417d338d98c8381701c5399b71412020abef31f8b9d9b",
    url = "https://modular-bazel-artifacts-public.s3.amazonaws.com/artifacts/rocshmem//a94edf6e52add4efe8c417d338d98c8381701c5399b71412020abef31f8b9d9b/rocshmem.tar.gz",
)

http_archive(
    name = "nvshmem_prebuilt",
    build_file_content = """
package(default_visibility = ["//visibility:public"])

# This is the device-side bitcode that is linked into shmem.mojopkg.
filegroup(
    name = "device",
    srcs = ["lib/libnvshmem_device.bc"],
)

# This has three purposes:
#  - enable bazel to dlopen the libs from test runfiles
#  - copy the libs to python wheel `site-packages/modular/lib` folder
#  - relink and copy the libs to conda package `lib` folder
genrule(
    name = "host",
    srcs = glob(["lib/*.so*"]),
    outs = [
        "libnvshmem_host.so.3",
        "nvshmem_bootstrap_mpi.so.3",
        "nvshmem_transport_ibrc.so.3",
        "nvshmem_transport_ibgda.so.3",
    ],
    cmd = '''
        # Copy all lib/*.so* files from our prebuilt tar
        for src in $(SRCS); do
            cp -L $$src $(@D)
        done
    ''',
)
""",
    # commit: https://github.com/modularml/NVSHMEM/commit/de1259385e38e373b7d71fd390f9f5468140d061
    sha256 = "b0808fbba808fc0742f60c16a7062c1ff35655cfe796574bf287009313d7f0c1",
    url = "https://modular-bazel-artifacts-public.s3.amazonaws.com/artifacts/nvshmem/3.4.5/b0808fbba808fc0742f60c16a7062c1ff35655cfe796574bf287009313d7f0c1/nvshmem.tar.gz",
)

mojo = use_extension("@rules_mojo//mojo:extensions.bzl", "mojo")
mojo.gpu_toolchains(
    # nvidia-smi / amd-smi output -> GPU name, empty string to ignore GPU
    gpu_mapping = {
        " A10G": "a10",
        "A100-": "a100",
        " H100": "h100",
        " H200": "h200",
        " L4": "l4",
        " Ada ": "l4",
        " A3000 ": "a3000",
        "B100": "b100",
        "B200": "b200",
        " RTX 5090": "rtx5090",
        "Laptop GPU": "",
        "RTX 4070 Ti": "",
        "RTX 4080 SUPER": "",
        "RTX 4090": "rtx4090",
        "NVIDIA GeForce RTX 3090": "rtx3090",
        "NVIDIA GeForce GTX 1080 Ti": "gtx1080ti",
        "NVIDIA GeForce GTX 1060": "gtx1060",
        "NVIDIA GeForce GTX 970": "gtx970",
        "MI300X": "mi300x",
        "MI325": "mi325",
        "MI355": "mi355",
        "Navi 31": "W7900",
        "Navi 31 [Radeon Pro W7900]": "W7900",
        "AMD Radeon RX 6900 XT": "rx6900xt",
        "AMD Radeon PRO W7900": "W7900",
        "AMD Radeon Pro W7900": "W7900",
        "Metal 1": "",  # Unsupported, macOS updates on any hardware get you to Metal 3+
        "Metal 2": "",  # Unsupported, macOS updates on any hardware get you to Metal 3+
        "Metal 3": "metal3",  # macOS 15
        "Metal 4": "metal4",  # macOS 26
    },
    # GPU name -> target accelerator
    supported_gpus = {
        "780M": "amdgpu:gfx1103",
        "W7900": "amdgpu:gfx1100",
        "a10": "nvidia:86",
        "a100": "nvidia:80",
        "a3000": "nvidia:86",
        "b100": "nvidia:100a",
        "b200": "nvidia:100a",
        "h100": "nvidia:90a",
        "h200": "nvidia:90a",
        "l4": "nvidia:89",
        "mi300x": "amdgpu:gfx942",
        "mi325": "amdgpu:gfx942",
        "mi355": "amdgpu:gfx950",
        "rtx3090": "nvidia:86",
        "gtx970": "nvidia:52",
        "gtx1080ti": "nvidia:61",
        "gtx1060": "nvidia:61",
        "rtx4090": "nvidia:89",
        "rtx5090": "nvidia:120a",
        "rx6900xt": "amdgpu:gfx1030",
        "metal3": "metal:3",
        "metal4": "metal:4",
    },
)
use_repo(mojo, "mojo_gpu_toolchains", "mojo_host_platform")

http_archive(
    name = "uv_linux_aarch64",
    build_file_content = 'exports_files(["uv"])',
    sha256 = "f177397625eb7ca184a01a954c7d487d1cafee68e53cc8f2789a02f75c347a68",
    strip_prefix = "uv-aarch64-unknown-linux-musl",
    url = "https://github.com/astral-sh/uv/releases/download/0.9.7/uv-aarch64-unknown-linux-musl.tar.gz",
)

http_archive(
    name = "uv_linux_x86_64",
    build_file_content = 'exports_files(["uv"])',
    sha256 = "b26fcc8dfa1c39b5a5613445af3be3eefda45d9a39359bee271eafe34913583e",
    strip_prefix = "uv-x86_64-unknown-linux-gnu",
    url = "https://github.com/astral-sh/uv/releases/download/0.9.7/uv-x86_64-unknown-linux-gnu.tar.gz",
)

http_archive(
    name = "uv_darwin_aarch64",
    build_file_content = 'exports_files(["uv"])',
    sha256 = "35572b9619fc14d67fc1cd72582c3cfc5c9c66d97f310192e04f26fb3fe96005",
    strip_prefix = "uv-aarch64-apple-darwin",
    url = "https://github.com/astral-sh/uv/releases/download/0.9.7/uv-aarch64-apple-darwin.tar.gz",
)

bazel_dep(name = "sysroot-jammy-aarch64")
archive_override(
    module_name = "sysroot-jammy-aarch64",
    integrity = "sha256-62kBJgGFqcDoEES1rkFYTaZJ0Zgjz0P8HIls6t5zUKI=",
    urls = [
        "https://modular-bazel-artifacts-public.s3.amazonaws.com/artifacts/sysroot-jammy-aarch64/7/eb6901260185a9c0e81044b5ae41584da649d19823cf43fc1c896ceade7350a2/sysroot-jammy-aarch64.tar.xz",
    ],
)

bazel_dep(name = "sysroot-jammy-x86_64")
archive_override(
    module_name = "sysroot-jammy-x86_64",
    integrity = "sha256-fVF6U6G1kb7+PM2HS6KM5mKYRF93S6gEg8l7tYIV+nc=",
    urls = [
        "https://modular-bazel-artifacts-public.s3.amazonaws.com/artifacts/sysroot-jammy-x86_64/7/7d517a53a1b591befe3ccd874ba28ce66298445f774ba80483c97bb58215fa77/sysroot-jammy-x86_64.tar.xz",
    ],
)

http_archive(
    name = "clang-linux-x86_64",
    build_file = "//bazel/public-patches:clang.BUILD",  # TODO: Delete next bump
    sha256 = "4ec4349717c3e6c7ffa0777b6f7e8987af039e05f0a32af5316c3dd730496651",
    url = "https://modular-bazel-artifacts-public.s3.amazonaws.com/artifacts/llvm-linux-x86_64/20.1.8/4ec4349717c3e6c7ffa0777b6f7e8987af039e05f0a32af5316c3dd730496651/llvm-linux-x86_64.tar.xz",
)

http_archive(
    name = "clang-linux-aarch64",
    build_file = "//bazel/public-patches:clang.BUILD",  # TODO: Delete next bump
    sha256 = "5bda1756f6cfb7a3472b5a885acb8d747040662305922eb809154481f1222189",
    url = "https://modular-bazel-artifacts-public.s3.amazonaws.com/artifacts/llvm-linux-aarch64/20.1.8/5bda1756f6cfb7a3472b5a885acb8d747040662305922eb809154481f1222189/llvm-linux-aarch64.tar.xz",
)

http_archive(
    name = "clang-macos",
    build_file = "//bazel/public-patches:clang.BUILD",  # TODO: Delete next bump
    sha256 = "a871653a114657136ae0dc25d979046a800aa30d8951e82b00eb5720caf9b4a8",
    url = "https://modular-bazel-artifacts-public.s3.amazonaws.com/artifacts/llvm-macos/20.1.8/a871653a114657136ae0dc25d979046a800aa30d8951e82b00eb5720caf9b4a8/llvm-macos.tar.xz",
)
